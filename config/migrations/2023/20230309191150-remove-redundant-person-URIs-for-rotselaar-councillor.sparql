PREFIX owl: <http://www.w3.org/2002/07/owl#>
##
# Remove redundant person URIs
# mandatarissen will point to the wrong URI. These need update.
##

DELETE {
  GRAPH ?g {
    ?s ?p ?o .
    ?mandataris <http://data.vlaanderen.be/ns/mandaat#isBestuurlijkeAliasVan> ?s .
  }
}
INSERT {
  GRAPH ?g {
    # Keep a reference from the redundant URIs to the corrent one since those URIs could be referenced somewhere else instead
    # of just inside our database.
    ?s owl:sameAs <http://data.lblod.info/id/personen/233a36a6-0435-4b73-88a2-93cbc95359bf> .
    ?mandataris <http://data.vlaanderen.be/ns/mandaat#isBestuurlijkeAliasVan> <http://data.lblod.info/id/personen/233a36a6-0435-4b73-88a2-93cbc95359bf> .
  }
}
WHERE {
  VALUES ?s {
    <http://data.lblod.info/id/personen/63B7E5BDDF5E04E2FA93C5E0>
    <http://data.lblod.info/id/personen/63B7E952DF5E04E2FA93C5ED>
  }

  GRAPH ?g {
    ?s ?p ?o .
    ?mandataris <http://data.vlaanderen.be/ns/mandaat#isBestuurlijkeAliasVan> ?s.
  }

  FILTER (?g NOT IN (
    <http://redpencil.data.gift/id/deltas/producer/loket-leidinggevenden-producer> ,
    <http://redpencil.data.gift/id/deltas/producer/loket-mandatarissen-producer> ,
    <http://redpencil.data.gift/id/deltas/producer/loket-submissions> ,
    <http://redpencil.data.gift/id/deltas/producer/loket-persons-sensitive> ,
    <http://redpencil.data.gift/id/deltas/producer/loket-subsidies>
  ))
}