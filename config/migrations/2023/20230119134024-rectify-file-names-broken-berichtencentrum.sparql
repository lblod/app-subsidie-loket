DELETE {
  GRAPH ?g {
      ?physicalFile <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#fileName> ?physicalFileName.
      ?logicalFile <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#fileName> ?fileName.
  }
}
INSERT {
  GRAPH ?g {
      ?physicalFile <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#fileName> ?newPhysicalFileName.
      ?logicalFile <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#fileName> ?newFileName.
  }
}
WHERE {

 ?s a <http://schema.org/Message>;
  <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#hasPart> ?logicalFile.

 GRAPH ?g {
   ?logicalFile a <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#FileDataObject>;
     <http://purl.org/dc/terms/format> ?format;
     <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#fileName> ?fileName;
     <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#fileName> "Documenten".

    ?physicalFile <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#dataSource> ?logicalFile;
      <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#fileName> ?physicalFileName.
  }

   BIND(
     IF(?format = "application/pdf",
         CONCAT(STR(?fileName), ".pdf"),
           IF(?format = "application/zip; charset=binary",
              CONCAT(STR(?fileName), ".zip"),
              IF(?format = "application/vnd.openxmlformats-officedocument.wordprocessingml.document",
                 CONCAT(STR(?fileName), ".docx"),
                 CONCAT(STR(?fileName), ".msg")
              )
           )
     )
     as ?newFileName
   )

   BIND(
     IF(?format = "application/pdf",
         CONCAT(STRBEFORE(STR(?physicalFileName), "."), ".pdf"),
           IF(?format = "application/zip; charset=binary",
              CONCAT(STRBEFORE(STR(?physicalFileName), "."), ".zip"),
              IF(?format = "application/vnd.openxmlformats-officedocument.wordprocessingml.document",
                 CONCAT(STRBEFORE(STR(?physicalFileName), "."), ".docx"),
                 CONCAT(STRBEFORE(STR(?physicalFileName), "."), ".msg")
              )
           )
     )
     as ?newPhysicalFileName
   )
}