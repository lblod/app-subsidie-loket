PREFIX meb:     <http://rdf.myexperiment.org/ontologies/base/>
PREFIX adms:    <http://www.w3.org/ns/adms#>
PREFIX prov:    <http://www.w3.org/ns/prov#>
PREFIX ext:     <http://mu.semte.ch/vocabularies/ext/>
PREFIX eli:     <http://data.europa.eu/eli/ontology#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX pav:     <http://purl.org/pav/>
prefix mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX schema:  <http://schema.org/>
PREFIX dct:     <http://purl.org/dc/terms/>

INSERT {
  GRAPH ?h {
    # Flag for export
    ?submission schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> . 
    ?submissionDocument schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?submissionDocumentStatus schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?formData schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?props schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?submissionDocumentFile schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
  }

  GRAPH ?j {
    ?concept schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    # Get bestuurseenheden by bestuursorgaan & -eenheid classificatie code
    # POLITIEZONE & HULPVERLENINGSZONE
    VALUES (?bestuursorgaanClassificatieCode ?bestuurseenheidClassificatieCode) {
        ( <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/a9e30e31-0cd2-4f4a-9352-545c5d43be83> <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/ea446861-2c51-45fa-afd3-4e4a37b71562> )
         ( <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/1afce932-53c1-46d8-8aab-90dcc331e67d> <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/a3922c6d-425b-474f-9a02-ffb71a436bfc> )
    }

    ?bestuursorgaan mandaat:isTijdspecialisatieVan/besluit:classificatie ?bestuursorgaanClassificatieCode .
    ?bestuurseenheid besluit:classificatie ?bestuurseenheidClassificatieCode .
  }
  
  GRAPH ?g {
    # Get all submissions of type BesluitenLijst & status SENT
    ?submission a meb:Submission ;
      adms:status <http://lblod.data.gift/concepts/9bd8d86d-bb10-4456-a84e-91e9507c374c> ; # SENT
      prov:generated/ext:decisionType <https://data.vlaanderen.be/id/concept/BesluitDocumentType/3fa67785-ffdc-4b30-8880-2b99d97b4dee> ; # Besluitenlijst
      prov:generated ?formData ;
      dct:subject ?submissionDocument;
      pav:createdBy ?bestuurseenheid .

     # Ignore already flagged submissions
     FILTER NOT EXISTS {
       ?submission schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
     }

    # Get file refs
    ?submissionDocument <http://purl.org/dc/terms/source> ?submissionDocumentFile .

    # Give me formData props limited to following list of types
    ?formData ?formDataPred ?props.

    FILTER (?formDataPred IN (
      <http://lblod.data.gift/vocabularies/besluit/authenticityType>, 
      <http://mu.semte.ch/vocabularies/ext/regulationType>,
      <http://mu.semte.ch/vocabularies/ext/decisionType>,
      <http://mu.semte.ch/vocabularies/ext/taxType>,
      <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#hasPart>
    ))    

    BIND(?g as ?h)
    BIND(?i as ?j)
  }
}
