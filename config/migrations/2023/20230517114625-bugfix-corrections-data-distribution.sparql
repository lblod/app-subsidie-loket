# This migration corrects the broken data in the vendor data distrubtion graph
# Instead of the granular correction, this is the occasion to fill all the graphs with
# all data submitted by vendors. So, easier integration for them.

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX muAccount: <http://mu.semte.ch/vocabularies/account/>
PREFIX wotSec: <https://www.w3.org/2019/wot/security#>
PREFIX lblodAuth: <http://lblod.data.gift/vocabularies/authentication/>
PREFIX pav: <http://purl.org/pav/>
PREFIX session: <http://mu.semte.ch/vocabularies/session/>
PREFIX oslc: <http://open-services.net/ns/core#>
PREFIX dct: <http://purl.org/dc/terms/>

# Start by deleting all the data in the vendor distribution data so far
DELETE {
  GRAPH ?g {
    ?s ?p ?o.
  }
}
WHERE {
   GRAPH ?g {
     ?s a <http://rdf.myexperiment.org/ontologies/base/Submission>;
       ?p ?o.
  }
  FILTER(STRSTARTS(STR(?g), "http://mu.semte.ch/graphs/vendors/"))
}

;

# proceed to syncing all (relevant) data submitted by vendors so far

INSERT {
  GRAPH ?vendorGraph {
    ?submission ?submissionP ?submissionO.
  }
}
WHERE {

  ?submission a <http://rdf.myexperiment.org/ontologies/base/Submission>;
    pav:createdBy ?organisation;
    pav:providedBy ?vendor;
    ?submissionP ?submissionO.
  ?vendor
    muAccount:canActOnBehalfOf ?organisation ;
    mu:uuid ?vendorId .
  ?organisation
    mu:uuid ?organisationId .

   BIND(IRI(CONCAT("http://mu.semte.ch/graphs/vendors/", STR(?vendorId), "/", STR(?organisationId))) as ?vendorGraph)
}