PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX task: <http://redpencil.data.gift/vocabularies/tasks/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX oslc: <http://open-services.net/ns/core#>
PREFIX cogs: <http://vocab.deri.ie/cogs#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX nmo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nmo#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX schema: <http://schema.org/>
PREFIX meb: <http://rdf.myexperiment.org/ontologies/base/>
PREFIX melding: <http://lblod.data.gift/vocabularies/automatische-melding/>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>

DELETE {
  GRAPH ?g {
    ?submission ?psubmission ?osubmission .
    ?submissionDocument ?psubmissionDocument ?osubmissionDocument .
  }
}
WHERE {
  GRAPH ?g {
    ?submission <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#hasPart> ?remoteUrl ;
      <http://purl.org/pav/createdBy> ?bestuur ;
      dct:subject ?submissionDocument .

    ?remoteUrl a nfo:RemoteDataObject ;
      adms:status <http://lblod.data.gift/file-download-statuses/failure> ;
      nie:url ?url .

    FILTER NOT EXISTS { ?container task:hasRemoteUrl ?remoteUrl . }

    ?submission ?psubmission ?osubmission .

    ?submissionDocument a ext:SubmissionDocument ;
      ?psubmissionDocument ?osubmissionDocument .
  }

  GRAPH ?h {
    ?bestuur <http://data.vlaanderen.be/ns/besluit#classificatie> ?classification .
    VALUES ?classification {
      <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/f9cac08a-13c1-49da-9bcb-f650b0604054> # CB
      <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/66ec74fd-8cfc-4e16-99c6-350b35012e86> # EB
      <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/36372fad-0358-499c-a4e3-f412d2eae213> # RO
    }
  }
}
