# Delete data with known type

DELETE {
  GRAPH ?g {
    ?s ?p ?o .
  }
  GRAPH ?h {
    ?a ?b ?s .
  }
} WHERE {
  GRAPH ?g {
    ?s a ?type ;
      ?p ?o .

    FILTER (?type IN (
      # deprecated types in submissions
      <http://mu.semte.ch/vocabularies/ext/supervision/InzendingVoorToezicht>,
      <http://mu.semte.ch/vocabularies/ext/FormSolution>,
      # unused tasks
      <http://mu.semte.ch/vocabularies/ext/export/Task>,
      <http://mu.semte.ch/vocabularies/ext/supervision/reporting/Task>,
      # last report generated on 18/08/19, unused
      <http://mu.semte.ch/vocabularies/ext/supervision/reporting/Report>,
      # first version of the subsidies model, deprecated now
      <http://data.vlaanderen.be/ns/subsidie#Aanvraag>,
      # used for a test, a long time ago
      <http://schema.org/PostalAddres>
    ))
  }
  OPTIONAL {
    GRAPH ?h {
      ?a ?b ?s .
    }
  }
}
