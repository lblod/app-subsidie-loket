PREFIX schema: <http://schema.org/>
PREFIX pav: <http://purl.org/pav/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX lblodBesluit: <http://lblod.data.gift/vocabularies/besluit/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX eli: <http://data.europa.eu/eli/ontology#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dct: <http://purl.org/dc/terms/>

INSERT {
  GRAPH ?h {
    ?attachment schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
  }
}
WHERE {
  {
    ?submission schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> ;
      nie:hasPart |
      nie:hasPart/^nie:dataSource |
      dct:hasPart/^nie:dataSource |
      nie:hasPart/^nie:dataSource/^nie:dataSource |
      prov:generated/dct:hasPart |
      prov:generated/dct:hasPart/nie:dataSource ?attachment .


    ?submission pav:createdBy ?bestuurseenheid;
      prov:generated ?form.

    ?form
        ext:decisionType <https://data.vlaanderen.be/id/concept/BesluitDocumentType/3fa67785-ffdc-4b30-8880-2b99d97b4dee> ; # Besluitenlijst
        eli:passed_by ?bestuursorgaan .


    VALUES (?bestuursorgaanClassificatieCode ?bestuurseenheidClassificatieCode) {
        ( <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000007> <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000002> )
         ( <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/b52094ff-21a2-4da8-8dbe-f513365d1528> <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/cc4e2d67-603b-4784-9b61-e50bac1ec089> )
         ( <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/013cc838-173a-4657-b1ae-b00c048df943> <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/cc4e2d67-603b-4784-9b61-e50bac1ec089> )
    }

    ?bestuursorgaan mandaat:isTijdspecialisatieVan/besluit:classificatie ?bestuursorgaanClassificatieCode .
    ?bestuurseenheid besluit:classificatie ?bestuurseenheidClassificatieCode .

  }
  UNION
  {
    ?submission schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> ;
      dct:subject/dct:source ?attachment.
    ?attachment dct:type ?type .

    FILTER (?type IN (
      <http://data.lblod.gift/concepts/meta-file-type>,
      <http://data.lblod.gift/concepts/form-data-file-type>,
      <http://data.lblod.gift/concepts/form-file-type>
    ))
  }

    GRAPH ?g {
      ?attachment a ?attachmentType .
    }
    FILTER(?g NOT IN (<http://redpencil.data.gift/id/deltas/producer/loket-submissions>))

  BIND(?g as ?h)
}