PREFIX cogs: <http://vocab.deri.ie/cogs#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

# WORKAROUND well known virtuoso issue (sprintf). TODO: migrate virtuoso
INSERT DATA {
  GRAPH <http://temp/graphs/a0c06709-2b13-49ec-828b-a6584b83a037> {
    <http://submission/eb047a2b-7353-4c4e-a245-b02bb463edde> a <http://rdf.myexperiment.org/ontologies/base/Submission>.
    <http://tasks/eb047a2b-7353-4c4e-a245-b02bb463edde> <http://www.w3.org/ns/prov#generated> <http://submission/eb047a2b-7353-4c4e-a245-b02bb463edde>;
        a <http://lblod.data.gift/vocabularies/automatische-melding/AutomaticSubmissionTask>;
         <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/automatische-melding-statuses/failure>.

    <http://tasks/eb047a2b-7353-4c4e-a245-b02bb463edde/1> <http://www.w3.org/ns/prov#generated> <http://submission/eb047a2b-7353-4c4e-a245-b02bb463edde>;
        a <http://lblod.data.gift/vocabularies/automatische-melding/AutomaticSubmissionTask>;
         <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/automatische-melding-statuses/ready-for-validation>.

    <http://tasks/eb047a2b-7353-4c4e-a245-b02bb463edde/2> <http://www.w3.org/ns/prov#generated> <http://submission/eb047a2b-7353-4c4e-a245-b02bb463edde>;
        a <http://lblod.data.gift/vocabularies/automatische-melding/AutomaticSubmissionTask>;
         <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/automatische-melding-statuses/not-started>.

    <http://tasks/eb047a2b-7353-4c4e-a245-b02bb463edde/3> <http://www.w3.org/ns/prov#generated> <http://submission/eb047a2b-7353-4c4e-a245-b02bb463edde>;
        a <http://lblod.data.gift/vocabularies/automatische-melding/AutomaticSubmissionTask>;
         <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/automatische-melding-statuses/successful-concept>.
  }
}

;

# convert failure state
DELETE {
  GRAPH ?g {
     ?task <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/automatische-melding-statuses/failure>.
  }
}
INSERT {
  GRAPH ?g {
     ?task a cogs:Job;
       <http://www.w3.org/ns/adms#status> <http://redpencil.data.gift/id/concept/JobStatus/failed>;
       skos:note "minimal conversion from automatische-melding:AutomaticSubmissionTask to cogs:Job".
  }
}
WHERE {
  GRAPH ?g {
    ?submission a <http://rdf.myexperiment.org/ontologies/base/Submission>.
    ?task <http://www.w3.org/ns/prov#generated> ?submission;
        a <http://lblod.data.gift/vocabularies/automatische-melding/AutomaticSubmissionTask>;
         <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/automatische-melding-statuses/failure>.
  }
}

;

# convert 'busy' state
DELETE {
  GRAPH ?g {
     ?task <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/automatische-melding-statuses/ready-for-validation>.
  }
}
INSERT {
  GRAPH ?g {
     ?task a cogs:Job;
       <http://www.w3.org/ns/adms#status> <http://redpencil.data.gift/id/concept/JobStatus/busy>;
       skos:note "minimal conversion from automatische-melding:AutomaticSubmissionTask to cogs:Job".
  }
}
WHERE {
  GRAPH ?g {
    ?submission a <http://rdf.myexperiment.org/ontologies/base/Submission>.
    ?task <http://www.w3.org/ns/prov#generated> ?submission;
        a <http://lblod.data.gift/vocabularies/automatische-melding/AutomaticSubmissionTask>;
         <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/automatische-melding-statuses/ready-for-validation>.
   }
}

;

# scheduled
DELETE {
  GRAPH ?g {
     ?task <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/automatische-melding-statuses/not-started>.
  }
}
INSERT {
  GRAPH ?g {
     ?task a cogs:Job;
       <http://www.w3.org/ns/adms#status> <http://redpencil.data.gift/id/concept/JobStatus/scheduled>;
       skos:note "minimal conversion from automatische-melding:AutomaticSubmissionTask to cogs:Job".
  }
}
WHERE {
  GRAPH ?g {
    ?submission a <http://rdf.myexperiment.org/ontologies/base/Submission>.
    ?task <http://www.w3.org/ns/prov#generated> ?submission;
        a <http://lblod.data.gift/vocabularies/automatische-melding/AutomaticSubmissionTask>;
         <http://www.w3.org/ns/adms#status> <http://lblod.data.gift/automatische-melding-statuses/not-started>.
   }
}
;
# success
DELETE {
  GRAPH ?g {
     ?task <http://www.w3.org/ns/adms#status> ?status.
  }
}
INSERT {
  GRAPH ?g {
     ?task a cogs:Job;
       <http://www.w3.org/ns/adms#status> <http://redpencil.data.gift/id/concept/JobStatus/success>;
       skos:note "minimal conversion from automatische-melding:AutomaticSubmissionTask to cogs:Job".
  }
}
WHERE {
  VALUES ?status {
    <http://lblod.data.gift/automatische-melding-statuses/successful-sent>
    <http://lblod.data.gift/automatische-melding-statuses/successful-concept>
  }
  GRAPH ?g {
    ?submission a <http://rdf.myexperiment.org/ontologies/base/Submission>.
    ?task <http://www.w3.org/ns/prov#generated> ?submission;
        a <http://lblod.data.gift/vocabularies/automatische-melding/AutomaticSubmissionTask>;
         <http://www.w3.org/ns/adms#status> ?status.
   }
}

;

# WORKAROUND cleanup
DELETE WHERE {
  GRAPH <http://temp/graphs/a0c06709-2b13-49ec-828b-a6584b83a037> {
   ?s ?p ?o.
  }
}
