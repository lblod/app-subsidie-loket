PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>
PREFIX person: <http://www.w3.org/ns/person#>
DELETE {
  GRAPH ?g {
    ?m org:holds ?mandaat.
  }
}
INSERT {
  GRAPH ?g {
    ?m org:holds ?correctedMandaat.
  }
}

WHERE {
  {
    SELECT DISTINCT ?m ?mandaat ?orgaan ?b ?role {
      ?m a mandaat:Mandataris;
        mandaat:start ?mStart;
        org:holds ?mandaat.

      FILTER NOT EXISTS {
         GRAPH <http://debug/mandatarissen/05-09-2022/wrong> {
           ?m a mandaat:Mandataris.
         }
       }

      GRAPH <http://mu.semte.ch/graphs/public> {
        ?mandaat a mandaat:Mandaat;
          org:role ?role.

        ?b a besluit:Bestuursorgaan;
          org:hasPost ?mandaat;
          mandaat:isTijdspecialisatieVan ?orgaan;
          mandaat:bindingStart ?bStart.
        ?b mandaat:bindingEinde ?bEnd.

      }
      FILTER(?mStart > ?bEnd)
    }
  }

  ?orgaan a besluit:Bestuursorgaan.

  ?correctedB mandaat:isTijdspecialisatieVan ?orgaan;
              org:hasPost ?correctedMandaat.

  FILTER NOT EXISTS {
    ?correctedB mandaat:bindingEinde ?correctedBEnd.
  }

  ?correctedMandaat org:role ?role.

  GRAPH ?g {
    ?m a mandaat:Mandataris
  }

  FILTER( ?g NOT IN (<http://redpencil.data.gift/id/deltas/producer/loket-mandatarissen-producer>))
}