PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>

DELETE {
  GRAPH ?g {
    ?account ext:sessionRole ?role.
  }
}
INSERT {
  GRAPH ?g {
    ?account ext:sessionRole
        "LoketLB-toezichtGebruiker",
        "LoketLB-eredienstBedienaarGebruiker",
        "LoketLB-eredienstMandaatGebruiker",
        "LoketLB-berichtenGebruiker".
  }
}
WHERE {
  VALUES ?type {
     erediensten:BestuurVanDeEredienst
     erediensten:CentraalBestuurVanDeEredienst
   }
  ?bestuurseenheid a ?type.

  ?persoon a foaf:Person;
    foaf:member ?bestuurseenheid;
    foaf:account ?account.

  GRAPH ?g {
   ?account a foaf:OnlineAccount;
      foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
       ext:sessionRole ?role.
  }
}