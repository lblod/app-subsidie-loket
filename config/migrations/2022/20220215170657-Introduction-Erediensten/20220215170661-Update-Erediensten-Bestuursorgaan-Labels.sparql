PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX lblodOrg: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bestuursorgaan
      skos:prefLabel ?prefLabelBestuursorgaan.
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?bestuursorgaan
      skos:prefLabel ?prefLabelBestuursorgaanNEW.
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    VALUES ?type {
      erediensten:BestuurVanDeEredienst
      erediensten:CentraalBestuurVanDeEredienst
    }
    # Selecting relevant data from an Eredienst
    ?eredienst
      a ?type;
      skos:prefLabel ?prefLabelEredienst.

    # Selecting relevant data from Bestuursoorgaan
    ?bestuursorgaan
      a besluit:Bestuursorgaan;
      org:classification ?classificationBestuursorgaan;
      skos:prefLabel ?prefLabelBestuursorgaan;
      besluit:bestuurt ?eredienst. # Link to current Eredienst

    # Selecting relevant data from Bestuursorgaan classificatie
    ?classificationBestuursorgaan
      a lblodOrg:BestuursorgaanClassificatieCode;
      skos:prefLabel ?prefLabelClassificatieBestuursorgaan.

    BIND(CONCAT(?prefLabelClassificatieBestuursorgaan, " ", ?prefLabelEredienst) AS ?prefLabelBestuursorgaanNEW)
  }
}