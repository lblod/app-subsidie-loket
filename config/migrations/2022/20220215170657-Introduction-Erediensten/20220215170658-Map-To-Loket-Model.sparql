PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX lblodOrg: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {

    # Map **tijdspecialisatie** Bestuursoorgaan
    ?tijdspecialisatie
      # From generiek:isTijdspecialisatieVan
      mandaat:isTijdspecialisatieVan ?bestuursorgaan.

    # Map Bestuursoorgaan classificatie
    ?classificationBestuursorgaan
      a ext:BestuursorgaanClassificatieCode.

    # Map Bestuursoorgaan
    ?bestuursorgaan
      # From org:classification
      besluit:classificatie ?classificationBestuursorgaan;
      skos:prefLabel ?newBestuursorgaanLabel.

    # Map Bestuurseenheid classificatie
    ?classificationEredienst a
        ext:BestuurseenheidClassificatieCode.

    # Map Eredienst
    ?eredienst
      a besluit:Bestuurseenheid;
      # From org:classification
      besluit:classificatie ?classificationEredienst;
      # From adms:identifier/generiek:gestructureerdeIdentificator/generiek:lokaleIdentificator
      dct:identifier ?KBONummer;
      # From adms:identifier/generiek:gestructureerdeIdentificator/generiek:lokaleIdentificator
      ext:kbonummer ?KBONummer.
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    VALUES ?type {
      erediensten:BestuurVanDeEredienst
      erediensten:CentraalBestuurVanDeEredienst
    }
    # Selecting relevant data from an Eredienst
    ?eredienst
      a ?type;
      org:classification ?classificationEredienst;
      # Nested resource in OP
      adms:identifier ?identifierEredienst;
      skos:prefLabel ?bestuurseenheidLabel.

    # Selecting relevant data from Identifier
    OPTIONAL {
       ?identifierEredienst
        a adms:Identifier;
        skos:notation "KBO nummer";
        generiek:gestructureerdeIdentificator/generiek:lokaleIdentificator ?KBONummer.
    }

    # Selecting relevant data from Eredienst classificatie
    ?classificationEredienst
      a lblodOrg:BestuurseenheidClassificatieCode.

    # Selecting relevant data from Bestuursoorgaan
    ?bestuursorgaan
      a besluit:Bestuursorgaan;
      org:classification ?classificationBestuursorgaan;
      # OP does not deliver a prefLabel
      # skos:prefLabel ?prefLabelBestuursorgaan;
      besluit:bestuurt ?eredienst. # Link to current Eredienst

    # Selecting relevant data from Bestuursorgaan classificatie
    ?classificationBestuursorgaan
      a lblodOrg:BestuursorgaanClassificatieCode;
      skos:prefLabel ?classificationBestuursorgaanLabel.

    # Selecting relevant data from **tijdspecialisatie** Bestuursoorgaan
    ?tijdspecialisatie
      a besluit:Bestuursorgaan;
      generiek:isTijdspecialisatieVan ?bestuursorgaan. # Link to current bestuursorgaan

    BIND(CONCAT(?classificationBestuursorgaanLabel, ?bestuurseenheidLabel) as ?newBestuursorgaanLabel)
  }
}