PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX organisatie: <https://data.vlaanderen.be/ns/organisatie#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX locn: <http://www.w3.org/ns/locn#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {

    # Map Eredienst
    ?eredienst
      # From org:hasPrimarySite/organisatie:bestaatUit/locn:adminUnitL2
      ext:inProvincie ?werkingsgebied.
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {

    # Map between Loket Werkingsgebied Concepts and OP Province Labels
    VALUES ( ?type ?werkingsgebied ?adminUnitL2Address ) {
      (  erediensten:BestuurVanDeEredienst <http://data.lblod.info/id/werkingsgebieden/f2bda6eb42c1ab85c4ef36588a043ac2bfeffa31bfb88bb7abc597f01efe270b> "Antwerpen" )
      (  erediensten:BestuurVanDeEredienst <http://data.lblod.info/id/werkingsgebieden/b6868be9ca6881b1020e6cff8d431531a1cff64b39dc92646a5173b3ee144516> "Limburg" )
      (  erediensten:BestuurVanDeEredienst <http://data.lblod.info/id/werkingsgebieden/a9a0cabd376a2b3a8eb838f15f6aeb1b63ffe49d527598994962e0d15ad2081c> "Oost-Vlaanderen" )
      (  erediensten:BestuurVanDeEredienst <http://data.lblod.info/id/werkingsgebieden/60a52229c92c1407e0549a573f820fb8a1f9fab5c82654b4b3493bc81be365de> "Vlaams-Brabant" )
      (  erediensten:BestuurVanDeEredienst <http://data.lblod.info/id/werkingsgebieden/9c503a712d448bc8b89c1f8802c207e30bd192bc8da3bf22509a547949a45301> "West-Vlaanderen" )
      (  erediensten:CentraalBestuurVanDeEredienst <http://data.lblod.info/id/werkingsgebieden/f2bda6eb42c1ab85c4ef36588a043ac2bfeffa31bfb88bb7abc597f01efe270b> "Antwerpen" )
      (  erediensten:CentraalBestuurVanDeEredienst <http://data.lblod.info/id/werkingsgebieden/b6868be9ca6881b1020e6cff8d431531a1cff64b39dc92646a5173b3ee144516> "Limburg" )
      (  erediensten:CentraalBestuurVanDeEredienst <http://data.lblod.info/id/werkingsgebieden/a9a0cabd376a2b3a8eb838f15f6aeb1b63ffe49d527598994962e0d15ad2081c> "Oost-Vlaanderen" )
      (  erediensten:CentraalBestuurVanDeEredienst <http://data.lblod.info/id/werkingsgebieden/60a52229c92c1407e0549a573f820fb8a1f9fab5c82654b4b3493bc81be365de> "Vlaams-Brabant" )
      (  erediensten:CentraalBestuurVanDeEredienst <http://data.lblod.info/id/werkingsgebieden/9c503a712d448bc8b89c1f8802c207e30bd192bc8da3bf22509a547949a45301> "West-Vlaanderen" )
    }

    # Selecting relevant data from an Eredienst
    ?eredienst
      a ?type;
      # Nested resource in OP
      org:hasPrimarySite ?primarySiteEredienst.

    # Selecting relevant data from Site
    ?primarySiteEredienst
      a org:Site;
      organisatie:bestaatUit/locn:adminUnitL2 ?adminUnitL2Address.
  }
}