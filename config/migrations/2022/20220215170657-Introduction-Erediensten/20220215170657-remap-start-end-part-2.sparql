# This is a bit a weird query, but essential we want to convert
# "2020-11-12T00:00:00"^^xsd:dateTime to => "2020-11-12"^^xsd:date
# Virtuoso does not like to do this operation at once, it seems.
# So we have to do a little detour through string manipulation.
# Furthermore, we have to deceive virtuoso, since it tries to smart bundle operations
# so we have to split the operation over multiple migrations files.
# This is part 2: update string data type to date
#
PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX lblodOrg: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?tijdspecialisatie
      mandaat:bindingStart ?start.
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?tijdspecialisatie
      mandaat:bindingStart ?newStart.
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    VALUES ?type {
      erediensten:BestuurVanDeEredienst
      erediensten:CentraalBestuurVanDeEredienst
    }

    ?eredienst
      a ?type.

    ?bestuursorgaan
      a besluit:Bestuursorgaan;
      besluit:bestuurt ?eredienst.

    # Selecting relevant data from **tijdspecialisatie** Bestuursoorgaan
    ?tijdspecialisatie
      a besluit:Bestuursorgaan;
      mandaat:bindingStart ?start;
      generiek:isTijdspecialisatieVan ?bestuursorgaan. # Link to current bestuursorgaan

    BIND(STRDT(STR(?start), xsd:date) AS ?newStart)

  }
}

;

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?tijdspecialisatie
      mandaat:bindingEinde ?einde.
  }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?tijdspecialisatie
      mandaat:bindingEinde ?newEinde.
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    VALUES ?type {
      erediensten:BestuurVanDeEredienst
      erediensten:CentraalBestuurVanDeEredienst
    }

    ?eredienst
      a ?type.

    ?bestuursorgaan
      a besluit:Bestuursorgaan;
      besluit:bestuurt ?eredienst.

    # Selecting relevant data from **tijdspecialisatie** Bestuursoorgaan
    ?tijdspecialisatie
      a besluit:Bestuursorgaan;
      mandaat:bindingEinde ?einde;
      generiek:isTijdspecialisatieVan ?bestuursorgaan. # Link to current bestuursorgaan

    BIND(STRDT(STR(?einde), xsd:date) AS ?newEinde)
  }
}