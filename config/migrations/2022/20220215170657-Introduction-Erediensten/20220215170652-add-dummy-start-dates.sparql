PREFIX erediensten: <http://data.lblod.info/vocabularies/erediensten/>
PREFIX lblodOrg: <http://lblod.data.gift/vocabularies/organisatie/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX generiek: <https://data.vlaanderen.be/ns/generiek#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?tijdspecialisatie
      mandaat:bindingStart ?newBindingStart;
      skos:note "[OP-IMPORT] added dummy start date".
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    VALUES ?type {
      erediensten:BestuurVanDeEredienst
      erediensten:CentraalBestuurVanDeEredienst
    }

    ?eredienst
      a ?type.

    ?bestuursorgaan
      a besluit:Bestuursorgaan;
      org:classification ?classificationBestuursorgaan;
      besluit:bestuurt ?eredienst. # Link to current Eredienst

    # Selecting relevant data from **tijdspecialisatie** Bestuursoorgaan
    ?tijdspecialisatie
      a besluit:Bestuursorgaan;
      generiek:isTijdspecialisatieVan ?bestuursorgaan. # Link to current bestuursorgaan

    FILTER NOT EXISTS {
      ?tijdspecialisatie mandaat:bindingStart ?bindingStart.
    }

    BIND(STRDT(STR("1971-11-03"), xsd:date) AS ?newBindingStart)
  }
}