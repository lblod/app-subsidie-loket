PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

DELETE {
  GRAPH ?h {
    ?bestuurseenheid
      lblodSubsidie:drawingRightEInclusion ?drawingRightEInclusion ;
      dbpedia:populationTotal ?populationTotal .
  }
}
INSERT {
  GRAPH ?g {
    <http://lblod.data.info/id/subsidy-measure-offers/0b5cae58-97fb-4982-9fb7-4cf660f003df>
      m8g:hasCriterion ?criterion ;
      subsidie:biedtAan ?subsidyAmount ;
      lblodSubsidie:heeftReeks <http://lblod.data.info/id/subsidy-measure-offer-series/666bb466-e0ba-4639-9260-d94e9d67e87f> .

  
    <http://data.lblod.info/id/criteria/ab426897-192e-47ea-9eb0-d92031aaa332> a m8g:Criterium ;
      mu:uuid "ab426897-192e-47ea-9eb0-d92031aaa332" ;
      dct:title "Toepasbaar op gemeentebesturen" ;
      dct:isPartOf <http://data.lblod.info/id/subsidy-procedural-steps/4fec426a-37ce-41f3-b9b3-74d0f9349856> ;
      m8g:fulfilledByRequirementGroup ?requirementGroup .
  }
  GRAPH ?h {
    ?subsidyAmount a subsidie:Subsidiebebdrag ;
      mu:uuid ?subsidyAmountUuid ;
      subsidie:maximumbedrag ?drawingRightEInclusion ;
      dct:requires ?requirementGroup .

    ?requirementGroup a m8g:RequirementGroup ;
      mu:uuid ?requirementGroupUuid ;
      dct:description ?requirementGroupDescription ;
      m8g:hasCriterionRequirement ?criterionRequirementBestuurseenheid ;
      m8g:hasCriterionRequirement ?criterionRequirementPopulation .

    ?criterionRequirementBestuurseenheid a m8g:CriterionRequirement ;
      mu:uuid ?criterionRequirementBestuurseenheidUuid ;
      dct:description "Het gemeentebestuur van toepassing" ;
      lblodSubsidie:isSatisfiableBy ?bestuurseenheid .

    ?criterionRequirementPopulation a m8g:CriterionRequirement ;
      mu:uuid ?criterionRequirementPopulationUuid ;
      dct:description "Het vereiste bevolkingsaantal" ;
      dbpedia:populationTotal ?populationTotal .
  }
} WHERE {
  GRAPH ?g {
    ?bestuurseenheid a besluit:Bestuurseenheid ;
      skos:prefLabel ?labelBestuurseenheid .
  }
  GRAPH ?h {
    ?bestuurseenheid
      lblodSubsidie:drawingRightEInclusion ?drawingRightEInclusion ;
      dbpedia:populationTotal ?populationTotal .
  }

  BIND(SHA256(CONCAT(STR(?populationTotal), "subsidyAmountEInclusion")) as ?subsidyAmountUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/subsidy-amounts/", ?subsidyAmountUuid)) AS ?subsidyAmount) .

  BIND(SHA256(CONCAT(STR(?populationTotal), "requirementEInclusion")) as ?requirementGroupUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/requirement-groups/", ?requirementGroupUuid)) AS ?requirementGroup) .
  BIND(CONCAT("Vereistengroup voor e-inclusiesubsidies - ", STR(?labelBestuurseenheid)) as ?requirementGroupDescription) .

  BIND(SHA256(CONCAT(STR(?populationTotal), "criterionRequirementBestuurseenheidEInclusion")) as ?criterionRequirementBestuurseenheidUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/criterion-requirements/", ?criterionRequirementBestuurseenheidUuid)) AS ?criterionRequirementBestuurseenheid) .

  BIND(SHA256(CONCAT(STR(?populationTotal), "criterionRequirementPopulationEInclusion")) as ?criterionRequirementPopulationUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/criterion-requirements/", ?criterionRequirementPopulationUuid)) AS ?criterionRequirementPopulation) .
}