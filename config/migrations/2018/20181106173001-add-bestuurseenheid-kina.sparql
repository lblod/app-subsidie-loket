PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

INSERT DATA {
  GRAPH <http://mu.semte.ch/graphs/public> {
    <http://data.lblod.info/id/bestuurseenheden/a9b3046e-331e-4668-b5cc-2da7bb09e354> a besluit:Bestuurseenheid ;
       mu:uuid "a9b3046e-331e-4668-b5cc-2da7bb09e354" ;
       skos:prefLabel "Regionaal Instituut voor Dringende Hulpverlening Krisisinfo-Netwerk-Antwerpen" ;
       dcterms:identifier "0445508132" ;
       ext:kbonummer "0445508132" ;
       besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/cc4e2d67-603b-4784-9b61-e50bac1ec089> ;
       besluit:werkingsgebied <http://data.lblod.info/id/werkingsgebieden/f2bda6eb42c1ab85c4ef36588a043ac2bfeffa31bfb88bb7abc597f01efe270b> .

   <http://data.lblod.info/id/bestuursorganen/319b266a-5a65-4283-8b35-90de314d12d7> besluit:bestuurt <http://data.lblod.info/id/bestuurseenheden/a9b3046e-331e-4668-b5cc-2da7bb09e354> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "319b266a-5a65-4283-8b35-90de314d12d7" ;
       skos:prefLabel "Raad van bestuur Kina" ;
       besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/013cc838-173a-4657-b1ae-b00c048df943> .

   <http://data.lblod.info/id/bestuursorganen/50e4593f-6331-481c-a5df-1978fae720e3> mandaat:isTijdspecialisatieVan <http://data.lblod.info/id/bestuursorganen/319b266a-5a65-4283-8b35-90de314d12d7> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "50e4593f-6331-481c-a5df-1978fae720e3" ;
       mandaat:bindingStart "1971-11-03"^^<http://www.w3.org/2001/XMLSchema#date> .

   <http://data.lblod.info/id/bestuursorganen/04d8494c-257b-4854-ad1b-fec1f67a2438> besluit:bestuurt <http://data.lblod.info/id/bestuurseenheden/a9b3046e-331e-4668-b5cc-2da7bb09e354> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "04d8494c-257b-4854-ad1b-fec1f67a2438" ;
       skos:prefLabel "Algemene vergadering Kina" ;
       besluit:classificatie  <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/b52094ff-21a2-4da8-8dbe-f513365d1528> .

   <http://data.lblod.info/id/bestuursorganen/650000b1-8352-4108-903d-e332a7c67eb1> mandaat:isTijdspecialisatieVan <http://data.lblod.info/id/bestuursorganen/04d8494c-257b-4854-ad1b-fec1f67a2438> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "650000b1-8352-4108-903d-e332a7c67eb1";
       mandaat:bindingStart "1971-11-03"^^<http://www.w3.org/2001/XMLSchema#date> .
  }
}

;

INSERT {
  GRAPH ?g {
     ?persoon a foaf:Person;
           mu:uuid ?uuidPersoon;
           foaf:firstName ?classificatie;
           foaf:familyName ?naam;
           foaf:member <http://data.lblod.info/id/bestuurseenheden/a9b3046e-331e-4668-b5cc-2da7bb09e354> ;
           foaf:account ?account.
     ?account a foaf:OnlineAccount;
           mu:uuid ?uuidAccount;
           foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
           ext:sessionRole "LoketLB-toezichtGebruiker", "LoketLB-bbcdrGebruiker", "LoketLB-mandaatGebruiker". 
  }
} WHERE {
     <http://data.lblod.info/id/bestuurseenheden/a9b3046e-331e-4668-b5cc-2da7bb09e354> a besluit:Bestuurseenheid;
     skos:prefLabel ?naam;
     besluit:classificatie/skos:prefLabel ?classificatie.
     BIND(CONCAT(?classificatie, " ", ?naam) as ?volledigeNaam)
     BIND(MD5(?volledigeNaam) as ?uuidPersoon)
     BIND(MD5(CONCAT(?volledigeNaam,"ACCOUNT")) as ?uuidAccount)
     BIND(IRI(CONCAT("http://data.lblod.info/id/persoon/", ?uuidPersoon)) AS ?persoon)
     BIND(IRI(CONCAT("http://data.lblod.info/id/account/", ?uuidAccount)) AS ?account)

     VALUES ?g {
        <http://mu.semte.ch/graphs/organizations/a9b3046e-331e-4668-b5cc-2da7bb09e354>
        <http://mu.semte.ch/graphs/public> 
     } 
}
