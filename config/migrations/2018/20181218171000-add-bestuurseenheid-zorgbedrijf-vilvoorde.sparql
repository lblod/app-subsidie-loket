PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

INSERT DATA {
  GRAPH <http://mu.semte.ch/graphs/public> {
    <http://data.lblod.info/id/bestuurseenheden/4b114b8e-11c5-4bc8-a3c2-4f5f05d7f430> a besluit:Bestuurseenheid ;
       mu:uuid "4b114b8e-11c5-4bc8-a3c2-4f5f05d7f430" ;
       skos:prefLabel "Zorgbedrijf Vilvoorde" ;
       dcterms:identifier "0697787118" ;
       ext:kbonummer "0697787118" ;
       besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/cc4e2d67-603b-4784-9b61-e50bac1ec089> ;
       besluit:werkingsgebied <http://data.lblod.info/id/werkingsgebieden/1f31500c62ad5117b572c6f33a2131a6ae2e7231e31a6d97df5ab712719075fe> .

   <http://data.lblod.info/id/bestuursorganen/618ec332-8afc-48e9-ac9d-f5f6ed5123b2> besluit:bestuurt <http://data.lblod.info/id/bestuurseenheden/4b114b8e-11c5-4bc8-a3c2-4f5f05d7f430> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "618ec332-8afc-48e9-ac9d-f5f6ed5123b2" ;
       skos:prefLabel "Raad van bestuur Zorgbedrijf Vilvoorde" ;
       besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/013cc838-173a-4657-b1ae-b00c048df943> .

   <http://data.lblod.info/id/bestuursorganen/618ef3d9-4c2a-412f-ad78-04a401e4988e> mandaat:isTijdspecialisatieVan <http://data.lblod.info/id/bestuursorganen/618ec332-8afc-48e9-ac9d-f5f6ed5123b2> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "618ef3d9-4c2a-412f-ad78-04a401e4988e" ;
       mandaat:bindingStart "1971-11-03"^^<http://www.w3.org/2001/XMLSchema#date> .

   <http://data.lblod.info/id/bestuursorganen/6f3fb0ed-c62d-4847-96a0-e64621c1f8a3> besluit:bestuurt <http://data.lblod.info/id/bestuurseenheden/4b114b8e-11c5-4bc8-a3c2-4f5f05d7f430> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "6f3fb0ed-c62d-4847-96a0-e64621c1f8a3" ;
       skos:prefLabel "Algemene vergadering Woonzorggroep Voorkempen" ;
       besluit:classificatie  <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/b52094ff-21a2-4da8-8dbe-f513365d1528> .

   <http://data.lblod.info/id/bestuursorganen/b4f6117c-08c0-4b62-b327-bc45a431386c> mandaat:isTijdspecialisatieVan <http://data.lblod.info/id/bestuursorganen/6f3fb0ed-c62d-4847-96a0-e64621c1f8a3> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "b4f6117c-08c0-4b62-b327-bc45a431386c";
       mandaat:bindingStart "2017-11-27"^^<http://www.w3.org/2001/XMLSchema#date> .
  }
}

;

INSERT {
  GRAPH ?g {
     ?persoon a foaf:Person;
           mu:uuid ?uuidPersoon;
           foaf:firstName ?classificatie;
           foaf:familyName ?naam;
           foaf:member <http://data.lblod.info/id/bestuurseenheden/4b114b8e-11c5-4bc8-a3c2-4f5f05d7f430> ;
           foaf:account ?account.
     ?account a foaf:OnlineAccount;
           mu:uuid ?uuidAccount;
           foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
           ext:sessionRole "LoketLB-toezichtGebruiker", "LoketLB-bbcdrGebruiker", "LoketLB-mandaatGebruiker". 
  }
} WHERE {
     <http://data.lblod.info/id/bestuurseenheden/4b114b8e-11c5-4bc8-a3c2-4f5f05d7f430> a besluit:Bestuurseenheid;
     skos:prefLabel ?naam;
     besluit:classificatie/skos:prefLabel ?classificatie.
     BIND(CONCAT(?classificatie, " ", ?naam) as ?volledigeNaam)
     BIND(MD5(?volledigeNaam) as ?uuidPersoon)
     BIND(MD5(CONCAT(?volledigeNaam,"ACCOUNT")) as ?uuidAccount)
     BIND(IRI(CONCAT("http://data.lblod.info/id/persoon/", ?uuidPersoon)) AS ?persoon)
     BIND(IRI(CONCAT("http://data.lblod.info/id/account/", ?uuidAccount)) AS ?account)

     VALUES ?g {
        <http://mu.semte.ch/graphs/organizations/4b114b8e-11c5-4bc8-a3c2-4f5f05d7f430>
        <http://mu.semte.ch/graphs/public> 
     } 
}
