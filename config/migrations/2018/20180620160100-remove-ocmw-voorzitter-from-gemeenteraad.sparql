PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>

WITH <http://mu.semte.ch/application>
DELETE {
  ?mandataris ?mandatarisP ?mandatarisO .
  ?mandaat ?mandaatP ?mandaatO .
  ?bestuursorgaan org:hasPost ?mandaat .
} INSERT {
  ?schepen mandaat:beleidsdomein ?beleidsdomein .
} WHERE {
  ?mandataris a mandaat:Mandataris ;
      org:holds ?mandaat ;
      ?mandatarisP ?mandatarisO .

  ?mandaat a mandaat:Mandaat ;
      org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000016> ; # OCMW voorzitter
      ?mandaatP ?mandaatO .

  ?bestuursorgaan a besluit:Bestuursorgaan ;
      org:hasPost ?mandaat ;
      mandaat:isTijdspecialisatieVan/besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000005> . # Gemeenteraad

  OPTIONAL {
    ?mandataris mandaat:beleidsdomein ?beleidsdomein ;
        mandaat:isBestuurlijkeAliasVan ?persoon .
    ?schepen mandaat:isBestuurlijkeAliasVan ?persoon ;
        org:holds/org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000014> . # Schepen
  }
}
