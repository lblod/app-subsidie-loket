PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

DELETE {
  GRAPH <http://mu.semte.ch/application> {
  ?s ?p ?o.
 }
}

INSERT {
  GRAPH ?newGraph {
  ?s ?p ?o.
  }
}

WHERE {
  GRAPH <http://mu.semte.ch/application> {

   ?s a mandaat:Fractie .
   ?s ?p ?o .
   ?s org:linkedTo ?bestuurseenheid .
   BIND(REPLACE(STR(?bestuurseenheid), "http://data.lblod.info/id/bestuurseenheden/", "") AS ?groupId)
   BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", ?groupId, "/LoketLB-mandaatGebruiker")) as ?newGraph)
  }

}