PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>

DELETE {
  GRAPH ?g {
    ?file dct:created ?created ;
      dct:modified ?modified .
  }
} INSERT {
  GRAPH ?g {
    ?file dct:created ?createdDT ;
      dct:modified ?modifiedDT .
  }
} WHERE {
  GRAPH ?g {
    ?step a lblodSubsidie:ApplicationStep ;
      dct:source ?file .
    ?file a nfo:FileDataObject ;
      dct:created ?created ;
      dct:modified ?modified .
  }

  BIND(STRDT(CONCAT(STR(?created), 'T00:00:00.000Z'), xsd:dateTime) as ?createdDT)
  BIND(STRDT(CONCAT(STR(?modified), 'T00:00:00.000Z'), xsd:dateTime) as ?modifiedDT)
}
