PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

DELETE {
   GRAPH ?g {
       ?org ext:submissionNotificationEmail ?mail ;
            ext:allowedSubmissionNotificationsMails ?allowed .
   }
} WHERE {
    ?org a besluit:Bestuurseenheid.
    GRAPH ?h {
        ?org ext:submissionNotificationEmail ?mail ;
             ext:allowedSubmissionNotificationsMails ?allowed .
    }
    BIND(?h as ?g)
}
;
INSERT {
    GRAPH ?targetGraph {
        ?org ext:submissionNotificationEmail ?mail ;
             ext:allowedSubmissionNotificationsMails ?allowed .
    }
} WHERE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?org a besluit:Bestuurseenheid.
    }
    GRAPH ?sourceGraph {
       ?org ext:mailAdresVoorNotificaties ?mail ;
            ext:wilMailOntvangen ?allowed .
    }
    FILTER contains(str(?sourceGraph),"LoketLB-berichtenGebruiker")
    BIND(IRI(replace(str(?sourceGraph), "LoketLB-berichtenGebruiker", "LoketLB-toezichtGebruiker")) as ?targetGraph)
}