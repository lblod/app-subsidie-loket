PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX transactie: <http://data.vlaanderen.be/ns/transactie#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX bikeSubsidie: <http://lblod.data.gift/vocabularies/subsidie/bicycle-infrastructure#>
PREFIX org: <http://schema.org/>

# Sprintf workaround
INSERT DATA {
  GRAPH <http://temp/sprintf/b92f4054-1585-4f6c-9789-379bdaf1cde0> {
    <http://fake/form/b92f4054-1585-4f6c-9789-379bdaf1cde0> a subsidie:SubsidiemaatregelConsumptie;
        transactie:isInstantieVan <http://lblod.data.gift/concepts/70cc4947-33a3-4d26-82e0-2e1eacd2fea2> ;
        dct:source <http://step/2/b92f4054-1585-4f6c-9789-379bdaf1cde0>,
                    <http://step/1/b92f4054-1585-4f6c-9789-379bdaf1cde0>.

    <http://step/1/b92f4054-1585-4f6c-9789-379bdaf1cde0> adms:status <http://lblod.data.gift/concepts/79a52da4-f491-4e2f-9374-89a13cde8ecd>.
    <http://step/2/b92f4054-1585-4f6c-9789-379bdaf1cde0> adms:status <http://lblod.data.gift/concepts/79a52da4-f491-4e2f-9374-89a13cde8ecd>.
  
  
    <http://fake/form/91269181-a2c3-40a7-acff-7952cf7cdaa0> a subsidie:SubsidiemaatregelConsumptie;
        transactie:isInstantieVan <http://lblod.data.gift/concepts/70cc4947-33a3-4d26-82e0-2e1eacd2fea2> ;
        <http://www.w3.org/2007/uwa/context/common.owl#active> <http://lblod.data.info/id/subsidie-application-flow-steps/f135ee39-fff1-4cae-9b87-bf3cd9098b3e> ;
        dct:source <http://step/2/91269181-a2c3-40a7-acff-7952cf7cdaa0>,
                    <http://step/1/91269181-a2c3-40a7-acff-7952cf7cdaa0>.

    <http://step/1/91269181-a2c3-40a7-acff-7952cf7cdaa0> adms:status <http://lblod.data.gift/concepts/9bd8d86d-bb10-4456-a84e-91e9507c374c>.
    <http://step/2/91269181-a2c3-40a7-acff-7952cf7cdaa0> adms:status <http://lblod.data.gift/concepts/79a52da4-f491-4e2f-9374-89a13cde8ecd>.
  }
}

;

# Case : Step 1 is in Concept, step 2 as well. Step 1 has not been sent, so no data can be there is
# step 2, we can just set the status to "New"

DELETE {
  GRAPH ?g {
    ?formStepTwo adms:status <http://lblod.data.gift/concepts/79a52da4-f491-4e2f-9374-89a13cde8ecd> . # concept
  }
}
INSERT {
  GRAPH ?g {
    ?formStepTwo adms:status <http://lblod.data.gift/concepts/6b7ae118-4653-48f2-9d9a-4712f8c30da9> . # new
  }
}
WHERE {
  GRAPH ?g {
    ?s a subsidie:SubsidiemaatregelConsumptie ;
      transactie:isInstantieVan <http://lblod.data.gift/concepts/70cc4947-33a3-4d26-82e0-2e1eacd2fea2> ; # bike subsidy
      dct:source ?formStepOne .
    ?formStepOne adms:status <http://lblod.data.gift/concepts/79a52da4-f491-4e2f-9374-89a13cde8ecd> ; # concept
      dct:isPartOf <http://lblod.data.info/id/subsidie-application-flow-steps/f135ee39-fff1-4cae-9b87-bf3cd9098b3e> .

    ?s dct:source ?formStepTwo .
    ?formStepTwo adms:status <http://lblod.data.gift/concepts/79a52da4-f491-4e2f-9374-89a13cde8ecd> ; # concept
      dct:isPartOf <http://lblod.data.info/id/subsidie-application-flow-steps/eb633b90fee29738872ee370ec24d60da792c9bdf5b97e125c7a34653d5e4397> .
  }
}

;

# Case : Step 1 is Sent but the SMC still has an active step linked (step 1) due to users closing the
# frontend too quickly. We make sure to set the status to "New" only when no data has been filled in step 2

DELETE {
  GRAPH ?g {
    ?formStepTwo adms:status <http://lblod.data.gift/concepts/79a52da4-f491-4e2f-9374-89a13cde8ecd> . # concept
    ?s <http://www.w3.org/2007/uwa/context/common.owl#active> <http://lblod.data.info/id/subsidie-application-flow-steps/f135ee39-fff1-4cae-9b87-bf3cd9098b3e> .
  }
}
INSERT {
  GRAPH ?g {
    ?formStepTwo adms:status <http://lblod.data.gift/concepts/6b7ae118-4653-48f2-9d9a-4712f8c30da9> . # new
    ?s <http://www.w3.org/2007/uwa/context/common.owl#active> <http://lblod.data.info/id/subsidie-application-flow-steps/eb633b90fee29738872ee370ec24d60da792c9bdf5b97e125c7a34653d5e4397> . # step 2
  }
}
WHERE {
  GRAPH ?g {
    ?s a subsidie:SubsidiemaatregelConsumptie ;
      transactie:isInstantieVan <http://lblod.data.gift/concepts/70cc4947-33a3-4d26-82e0-2e1eacd2fea2> ; # bike subsidy
      dct:source ?formStepOne ;
      dct:source ?formStepTwo .
    ?formStepOne adms:status <http://lblod.data.gift/concepts/9bd8d86d-bb10-4456-a84e-91e9507c374c> ; # vertuurd
      dct:isPartOf <http://lblod.data.info/id/subsidie-application-flow-steps/f135ee39-fff1-4cae-9b87-bf3cd9098b3e> . # step 1
    ?formStepTwo adms:status <http://lblod.data.gift/concepts/79a52da4-f491-4e2f-9374-89a13cde8ecd> ; # concept
      dct:isPartOf <http://lblod.data.info/id/subsidie-application-flow-steps/eb633b90fee29738872ee370ec24d60da792c9bdf5b97e125c7a34653d5e4397> .

    FILTER EXISTS { ?s <http://www.w3.org/2007/uwa/context/common.owl#active> <http://lblod.data.info/id/subsidie-application-flow-steps/f135ee39-fff1-4cae-9b87-bf3cd9098b3e> . } # step 1

    FILTER NOT EXISTS { ?formStepTwo nie:identifier ?dossierNummer . }
    FILTER NOT EXISTS { ?formStepTwo lblodSubsidie:projectName ?projectName . }
    FILTER NOT EXISTS { ?formStepTwo lblodSubsidie:decisionUpload ?file . }
    FILTER NOT EXISTS { ?formStepTwo org:bankAccount ?iban . }
    FILTER NOT EXISTS { ?formStepTwo bikeSubsidie:objectiveTable ?objectiveTable . }
    FILTER NOT EXISTS { ?formStepTwo bikeSubsidie:estimatedCostTable ?estimatedCostTable . }
    FILTER NOT EXISTS { ?formStepTwo org:contactPoint ?contactPoint . }
  }
}

;

DELETE {
  GRAPH <http://temp/sprintf/b92f4054-1585-4f6c-9789-379bdaf1cde0> {
    ?s ?p ?o.
  }
}
WHERE {
  GRAPH <http://temp/sprintf/b92f4054-1585-4f6c-9789-379bdaf1cde0> {
    ?s ?p ?o.
  }
}
