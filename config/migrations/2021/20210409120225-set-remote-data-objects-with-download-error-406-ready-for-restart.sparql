PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX task: <http://redpencil.data.gift/vocabularies/tasks/>
PREFIX nuao: <http://www.semanticdesktop.org/ontologies/2010/01/25/nuao#>
PREFIX rpioHTTP: <http://redpencil.data.gift/vocabularies/http/>


DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?s adms:status <http://lblod.data.gift/file-download-statuses/failure> ;
       ext:httpStatusCode 406 ;
       ext:cacheError ?error .

    ?download task:numberOfRetries ?retries .
  }
} INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?s adms:status <http://lblod.data.gift/file-download-statuses/ongoing> ;
       rpioHTTP:requestHeader <http://data.lblod.info/request-headers/29b14d06-e584-45d6-828a-ce1f0c018a8e> .

    ?download task:numberOfRetries 0 .
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?s a nfo:RemoteDataObject ;
       ext:httpStatusCode 406 ;
       ext:cacheError ?error;
       adms:status <http://lblod.data.gift/file-download-statuses/failure> .

    ?download nuao:involves ?s ;
              task:numberOfRetries ?retries .
  }
}