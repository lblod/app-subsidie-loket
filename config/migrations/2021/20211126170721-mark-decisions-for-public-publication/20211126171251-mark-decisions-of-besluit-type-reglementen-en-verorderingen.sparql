PREFIX meb:     <http://rdf.myexperiment.org/ontologies/base/>
PREFIX adms:    <http://www.w3.org/ns/adms#>
PREFIX prov:    <http://www.w3.org/ns/prov#>
PREFIX ext:     <http://mu.semte.ch/vocabularies/ext/>
PREFIX schema:  <http://schema.org/>
PREFIX dct:     <http://purl.org/dc/terms/>

INSERT {
  GRAPH ?h {
    ?sub schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?form schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?decisionType schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?submissionDocument schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?submissionDocumentStatus schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?authenticityType schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?taxType schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?chartOfAccount schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?file schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?submissionFile schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?submissionDocumentFile schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
    ?formDataFile schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
  }
  GRAPH ?j {
    ?concept schema:publication <http://lblod.data.gift/concepts/83f7b480-fcaf-4795-b603-7f3bce489325> .
  }
}
WHERE {
  GRAPH ?g {
    ?sub a meb:Submission ;
        adms:status <http://lblod.data.gift/concepts/9bd8d86d-bb10-4456-a84e-91e9507c374c> ; # SENT
        prov:generated ?form ;
        dct:subject ?submissionDocument .

    ?form <http://lblod.data.gift/vocabularies/besluit/authenticityType> |
          <http://mu.semte.ch/vocabularies/ext/regulationType> |
          <http://mu.semte.ch/vocabularies/ext/decisionType> |
          <http://mu.semte.ch/vocabularies/ext/taxType> |
          <http://data.europa.eu/eli/ontology#passed_by>/<http://data.vlaanderen.be/ns/mandaat#isTijdspecialisatieVan>/<http://data.vlaanderen.be/ns/besluit#bestuurt>/<http://data.vlaanderen.be/ns/besluit#classificatie> ?concept.

    VALUES ?regulationType {
        <https://data.vlaanderen.be/id/concept/BesluitType/4d8f678a-6fa4-4d5f-a2a1-80974e43bf34>
        <https://data.vlaanderen.be/id/concept/BesluitType/7d95fd2e-3cc9-4a4c-a58e-0fbc408c2f9b>
        <https://data.vlaanderen.be/id/concept/BesluitType/3bba9f10-faff-49a6-acaa-85af7f2199a3>
        <https://data.vlaanderen.be/id/concept/BesluitType/0d1278af-b69e-4152-a418-ec5cfd1c7d0b>
        <https://data.vlaanderen.be/id/concept/BesluitType/84121221-4217-40e3-ada2-cd1379b168e1>
        <https://data.vlaanderen.be/id/concept/BesluitType/efa4ec5a-b006-453f-985f-f986ebae11bc>
        <https://data.vlaanderen.be/id/concept/BesluitType/5ee63f84-2fa0-4758-8820-99dca2bdce7c>
        <https://data.vlaanderen.be/id/concept/BesluitType/25deb453-ae3e-4d40-8027-36cdb48ab738>
        <https://data.vlaanderen.be/id/concept/BesluitType/fb92601a-d189-4482-9922-ab0efc6bc935>
        <https://data.vlaanderen.be/id/concept/BesluitType/a8486fa3-6375-494d-aa48-e34289b87d5b>
        <https://data.vlaanderen.be/id/concept/BesluitType/4673d472-8dbc-4cea-b3ab-f92df3807eb3>
        <https://data.vlaanderen.be/id/concept/BesluitType/e8afe7c5-9640-4db8-8f74-3f023bec3241>
        <https://data.vlaanderen.be/id/concept/BesluitType/e8aee49e-8762-4db2-acfe-2d5dd3c37619>
        <https://data.vlaanderen.be/id/concept/BesluitType/ba5922c9-cfad-4b2e-b203-36479219ba56>
        <https://data.vlaanderen.be/id/concept/BesluitType/d7060f97-c417-474c-abc6-ef006cb61f41>
        <https://data.vlaanderen.be/id/concept/BesluitType/256bd04a-b74b-4f2a-8f5d-14dda4765af9>
    }

    ?form
        ext:decisionType <https://data.vlaanderen.be/id/concept/BesluitType/67378dd0-5413-474b-8996-d992ef81637a>; # Reglementen en verordeningen
        ext:regulationType ?regulationType .

    OPTIONAL { ?submissionDocument adms:status ?submissionDocumentStatus . }
    OPTIONAL { ?form <http://lblod.data.gift/vocabularies/besluit/authenticityType> ?authenticityType . }
    OPTIONAL { ?form <http://mu.semte.ch/vocabularies/ext/taxType> ?taxType . }
    OPTIONAL { ?form <http://lblod.data.gift/vocabularies/besluit/chartOfAccount> ?chartOfAccount . }
    OPTIONAL { ?form <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#hasPart> ?formDataFile . }
    OPTIONAL { ?sub <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#hasPart> ?submissionFile. }
    OPTIONAL { ?submissionDocument <http://purl.org/dc/terms/source> ?submissionDocumentFile }

    FILTER(?g NOT IN (<http://redpencil.data.gift/id/deltas/producer/loket-submissions>))
  }
  GRAPH ?i {
    ?concept <http://www.w3.org/2004/02/skos/core#inScheme> |
             <http://www.w3.org/2004/02/skos/core#topConceptOf> ?conceptScheme.

    FILTER ( ?conceptScheme IN (
      <http://lblod.data.gift/concept-schemes/5cecec47-ba66-4d7a-ac9d-a1e7962ca4e2>,
      <http://lblod.data.gift/concept-schemes/ac9bc402-c8e6-41fd-ad57-fad15622e560>,
      <https://data.vlaanderen.be/id/conceptscheme/BesluitType>,
      <https://data.vlaanderen.be/id/conceptscheme/BesluitDocumentType>,
      <http://data.vlaanderen.be/id/conceptscheme/BestuurseenheidClassificatieCode>,
      <http://lblod.data.gift/concept-schemes/b65b15ba-6755-4cd2-bd07-2c2cf3c0e4d3>,
      <http://lblod.data.gift/concept-schemes/c93ccd41-aee7-488f-86d3-038de890d05a>,
      <http://lblod.data.gift/concept-schemes/71e6455e-1204-46a6-abf4-87319f58eaa5>
    ))
  }
  BIND(?g as ?h)
  BIND(?i as ?j)
}
