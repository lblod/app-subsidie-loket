PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX xkos: <http://rdf-vocabulary.ddialliance.org/xkos#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

####################################################################
### TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error
INSERT DATA {
    GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5> {
        <http://dummy/application/6c68539a-f6de-4488-8c1b-1c182e07a142> a lblodSubsidie:ApplicationForm ;
            dct:source <config://versions/20201207135752/form.ttl> ,
                       <config://versions/20210113153339/form.ttl> ,
                       <config://versions/20210121152225-introduction-subsidy-form-contact-tracing/form.ttl> ,
                       <config://versions/20210309152225-introduction-subsidy-form-bicycle-infrastructure/form.ttl> ,
                       <config://versions/20210329141908-contact-tracing-checkbox-multi-selection/form.ttl> ,
                       <config://versions/20210412135856-bicycle-new-tables/form.ttl>;
            dct:isPartOf <http://lblod.data.info/id/subsidie-application-flow-steps/a4f532111679b2d1aada56fc4e71aa8217247d3ddd08f9024551e6f6f3f44827> ,
                         <http://lblod.data.info/id/subsidie-application-flow-steps/126ff5d8460a5d9af8f7428fcc156c18bbd6876e6c0c82b40e7630d1f322d86e> ,
                         <http://lblod.data.info/id/subsidie-application-flow-steps/2ed9cead49d5d4a1953836986c1274052a5ee27e9c3f9b001bc30f116aa3ef3d> ,
                         <http://lblod.data.info/id/subsidie-application-flow-steps/efa333b29bdde1d254880b031f876da7af88d5312673390a56516567c66c4a0e> ,
                         <http://lblod.data.info/id/subsidie-application-flow-steps/c4455818b29c0f84130efb4ab026486523695b95768feaf4f837c2586d2d3a13> ,
                         <http://lblod.data.info/id/subsidie-application-flow-steps/b7554a17132f69619e37bc48da6fdc04eacd3422a55c709afd361c827d2f2de2> ,
                         <http://lblod.data.info/id/subsidie-application-flow-steps/e020cf93ff2e79587ffde289d906f0b0a4d04945e49aecb83ca98880ef046613> .
    }
}
;
##########################################
# UPDATE old forms for: Emergency day-care
DELETE {
    GRAPH ?g {
        ?form dct:source ?sources .
    }
} INSERT {
    GRAPH ?g {
        ?form dct:source <config://forms/emergency-day-care/versions/20210420104208-initial-version/form.ttl>,
                         <config://forms/emergency-day-care/versions/20210420104208-initial-version/form.json>.
    }
} WHERE {
    GRAPH ?g {
        VALUES ?spec {
            <config://versions/20201207135752/form.ttl>
            <config://versions/20210113153339/form.ttl>
            <config://versions/20210329141908-contact-tracing-checkbox-multi-selection/form.ttl>
            <config://versions/20210121152225-introduction-subsidy-form-contact-tracing/form.ttl>
            <config://versions/20210309152225-introduction-subsidy-form-bicycle-infrastructure/form.ttl>
            <config://versions/20210412135856-bicycle-new-tables/form.ttl>
        }
        ?form a lblodSubsidie:ApplicationForm ;
              dct:source ?spec ;
              dct:source ?sources ;
              dct:isPartOf ?afs .
    }
    GRAPH ?public {
        VALUES ?measure {
            <http://lblod.data.gift/concepts/1df4b56a-3ccd-450d-93dc-317fda1ada38>
        }
        ?afs dct:isPartOf ?flow .
        ?flow xkos:belongsTo ?series .
        ?measure lblodSubsidie:heeftReeks ?series .
    }
}
;
######################################################################
# TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error
DELETE {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5>  {
    ?s ?p ?o .
  }
}
WHERE {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5>  {
    ?s ?p ?o .
  }
}