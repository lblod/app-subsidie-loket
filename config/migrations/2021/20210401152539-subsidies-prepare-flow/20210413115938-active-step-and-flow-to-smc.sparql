PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX common: <http://www.w3.org/2007/uwa/context/common.owl#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>

### TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error ###
INSERT DATA {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5> {
    <http://dumm/application/form> a lblodSubsidie:ApplicationForm.
    <http://dumm/application/form> <http://lblod.data.gift/vocabularies/subsidie/timeBlock> <http://dumm/reeks>.
    <http://dumm/smc> <http://purl.org/dc/terms/source> <http://dumm/application/form>.
    <http://dumm/smc> a <http://data.vlaanderen.be/ns/subsidie#SubsidiemaatregelConsumptie>.

    <http://dumm/reeks> <http://www.w3.org/2007/uwa/context/common.owl#active> <http://dumm/flow>.
    <http://dumm/flow> <http://rdf-vocabulary.ddialliance.org/xkos#next> <http://dumm/step>.
  }
}
### END OF TRICK ###

;

DELETE {
  GRAPH ?g {
     ?af <http://lblod.data.gift/vocabularies/subsidie/timeBlock> ?reeks.
  }
}
INSERT {
  GRAPH ?g {
    ?af dct:isPartOf ?step.
    ?smc cpsv:follows ?flow.
    ?smc common:active ?step.
  }
}
WHERE
{
  GRAPH ?g
  {
    ?af a lblodSubsidie:ApplicationForm.
    ?af <http://lblod.data.gift/vocabularies/subsidie/timeBlock> ?reeks.
    ?smc <http://purl.org/dc/terms/source> ?af.
    ?smc a <http://data.vlaanderen.be/ns/subsidie#SubsidiemaatregelConsumptie>.
  }

 ?reeks <http://www.w3.org/2007/uwa/context/common.owl#active> ?flow.
 ?flow <http://rdf-vocabulary.ddialliance.org/xkos#next> ?step.
}

;

### TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error ###
DELETE {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5>  {
    ?s ?p ?o .
  }
}
WHERE {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5>  {
    ?s ?p ?o .
  }
}
### END OF TRICK ###
