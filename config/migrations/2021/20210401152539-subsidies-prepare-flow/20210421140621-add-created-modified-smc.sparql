PREFIX mobiliteit: <https://data.vlaanderen.be/ns/mobiliteit#>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX gleif: <https://www.gleif.org/ontology/Base/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX common: <http://www.w3.org/2007/uwa/context/common.owl#>
PREFIX xkos: <http://rdf-vocabulary.ddialliance.org/xkos#>
PREFIX qb: <http://purl.org/linked-data/cube#>


### TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error ###
INSERT DATA {
  GRAPH <http://dummy/data/graph/31249388-3112-486d-b507-6c7c30c6aa53> {

    <http://foo/applicationForm/31249388-3112-486d-b507-6c7c30c6aa53> a lblodSubsidie:ApplicationForm.
    <http://foo/applicationForm/31249388-3112-486d-b507-6c7c30c6aa53> <http://mu.semte.ch/vocabularies/ext/lastModifiedBy> <http://dummy/df153a0a-71f8-4718-964a-f688269260e5>.
    <http://foo/applicationForm/31249388-3112-486d-b507-6c7c30c6aa53> <http://purl.org/dc/terms/creator> <http://dummy/df153a0a-71f8-4718-964a-f688269260e5>.
    <http://foo/applicationForm/31249388-3112-486d-b507-6c7c30c6aa53> <http://purl.org/dc/terms/created> <http://dummy/df153a0a-71f8-4718-964a-f688269260e5>.
    <http://foo/applicationForm/31249388-3112-486d-b507-6c7c30c6aa53> <http://purl.org/dc/terms/modified> <http://dummy/df153a0a-71f8-4718-964a-f688269260e5>.

    <http://foo/smc/906046a6-17fc-412e-b149-4966727eeef5> a <http://data.vlaanderen.be/ns/subsidie#SubsidiemaatregelConsumptie>.
    <http://foo/smc/906046a6-17fc-412e-b149-4966727eeef5> <http://purl.org/dc/terms/source> <http://foo/applicationForm/31249388-3112-486d-b507-6c7c30c6aa53>.
    <http://foo/smc/906046a6-17fc-412e-b149-4966727eeef5> <http://mu.semte.ch/vocabularies/ext/lastModifiedBy> <http://dummy/df153a0a-71f8-4718-964a-f688269260e5>.
    <http://foo/smc/906046a6-17fc-412e-b149-4966727eeef5> <http://purl.org/dc/terms/creator> <http://dummy/df153a0a-71f8-4718-964a-f688269260e5>.
    <http://foo/smc/906046a6-17fc-412e-b149-4966727eeef5> <http://purl.org/dc/terms/created> <http://dummy/df153a0a-71f8-4718-964a-f688269260e5>.
    <http://foo/smc/906046a6-17fc-412e-b149-4966727eeef5> <http://purl.org/dc/terms/modified> <http://dummy/df153a0a-71f8-4718-964a-f688269260e5>.
  }
}

;

DELETE {
  GRAPH ?g {
    ?smc <http://mu.semte.ch/vocabularies/ext/lastModifiedBy> ?lastModifierSmc.
    ?smc <http://purl.org/dc/terms/creator> ?creatorSmc.
    ?smc <http://purl.org/dc/terms/created> ?createdSmc.
    ?smc <http://purl.org/dc/terms/modified> ?modifiedSmc.
  }
}
INSERT {
  GRAPH ?g {
    ?smc <http://mu.semte.ch/vocabularies/ext/lastModifiedBy> ?lastModifier.
    ?smc <http://purl.org/dc/terms/creator> ?creator.
    ?smc <http://purl.org/dc/terms/created> ?created.
    ?smc <http://purl.org/dc/terms/modified> ?modified.
  }
}
WHERE {

  GRAPH ?g {
    ?s a lblodSubsidie:ApplicationForm.
    ?s <http://mu.semte.ch/vocabularies/ext/lastModifiedBy> ?lastModifier.
    ?s <http://purl.org/dc/terms/creator> ?creator.
    ?s <http://purl.org/dc/terms/created> ?created.
    ?s <http://purl.org/dc/terms/modified> ?modified.

    ?smc a <http://data.vlaanderen.be/ns/subsidie#SubsidiemaatregelConsumptie>.
    ?smc <http://purl.org/dc/terms/source> ?s.

    OPTIONAL {
      ?smc <http://mu.semte.ch/vocabularies/ext/lastModifiedBy> ?lastModifierSmc.
    }

    OPTIONAL {
       ?smc <http://purl.org/dc/terms/creator> ?creatorSmc.
    }

    OPTIONAL {
       ?smc <http://purl.org/dc/terms/created> ?createdSmc.
    }

    OPTIONAL {
      ?smc <http://purl.org/dc/terms/modified> ?modifiedSmc.
    }
  }
}

;

### TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error ###
DELETE {
  GRAPH <http://dummy/data/graph/31249388-3112-486d-b507-6c7c30c6aa53>  {
    ?s ?p ?o .
  }
}
WHERE {
  GRAPH <http://dummy/data/graph/31249388-3112-486d-b507-6c7c30c6aa53>  {
    ?s ?p ?o .
  }
}
### END OF TRICK ###