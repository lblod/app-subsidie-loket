# step : migrate concept shemes to full blown resources
PREFIX mobiliteit: <https://data.vlaanderen.be/ns/mobiliteit#>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX pav: <http://purl.org/pav/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX m8g: <http://data.europa.eu/m8g/>

### TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error ###
INSERT DATA {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5> {
    <http://dummy/1> <https://data.vlaanderen.be/ns/transactie#isInstantieVan> <http://dummy/2> .
    <http://dummy/application> a lblodSubsidie:ApplicationForm ;
      pav:createdBy <http://dummy/bestuurseenheid> ;
      lblodSubsidie:subsidyMeasure <http://dummy/aanbod> .
    <http://dummy/aanvraag> <http://www.w3.org/ns/prov#used> <http://dummy/application>.
    <http://dummy/consumptie> prov:wasGeneratedBy <http://dummy/aanvraag> ;
      m8g:hasParticipation <http://dummy/participatie> .
    <http://dummy/par/eenheid> m8g:playsRole <http://dummy/participatie> .
  }
}
### END OF TRICK ###

;

DELETE {
  GRAPH ?g {
    ?s a ?type.
    ?s <http://mu.semte.ch/vocabularies/ext/submissionPeriod> ?period;
       <http://www.w3.org/2004/02/skos/core#inScheme> ?aanbod;
       <http://www.w3.org/2004/02/skos/core#definition> ?definition.
    ?s  <http://www.w3.org/2004/02/skos/core#prefLabel> ?label.
  }
}

INSERT {
  GRAPH ?g {
    ?s a lblodSubsidie:SubsidiemaatregelAanbodReeks.
    ?s dct:title ?title.
    ?s dct:description ?label.
    ?aanbod lblodSubsidie:heeftReeks ?s.
  }
}
WHERE {
  GRAPH ?g {
    ?s a <http://www.w3.org/2000/01/rdf-schema#Class>, <http://www.w3.org/2004/02/skos/core#Concept>, <https://www.gleif.org/ontology/Base/Period>.
    ?s a ?type.
    ?s <http://mu.semte.ch/vocabularies/ext/submissionPeriod> ?period;
       <http://www.w3.org/2004/02/skos/core#inScheme> ?aanbod;
       <http://www.w3.org/2004/02/skos/core#definition> ?definition;
       <https://www.gleif.org/ontology/Base/hasEnd> ?end;
       <https://www.gleif.org/ontology/Base/hasStart> ?start.
    OPTIONAL { ?s <http://purl.org/dc/terms/title> ?title. }
    OPTIONAL { ?s <http://www.w3.org/2004/02/skos/core#prefLabel> ?label. }
  }
}

;

DELETE {
  GRAPH ?g {
   ?s a ?type;
     <http://www.w3.org/2004/02/skos/core#definition> ?definition;
     <http://www.w3.org/2004/02/skos/core#inScheme> ?scheme.
  }
}

INSERT {
  GRAPH ?g {
    ?s a <http://data.vlaanderen.be/ns/subsidie#SubsidiemaatregelAanbod>.
  }
}
WHERE {
  GRAPH ?g {
   ?s <http://www.w3.org/1999/02/22-rdf-syntax-ns#type> <http://data.vlaanderen.be/ns/subsidie#SubsidiemaatregelAanbod>;
     a ?type;
     <http://www.w3.org/2004/02/skos/core#definition> ?definition;
     <http://www.w3.org/2004/02/skos/core#inScheme> ?scheme.
  }
}

;

DELETE {
  GRAPH ?g {
    ?s ?p ?o.
  }
}
WHERE {
  GRAPH ?g {
    BIND( <http://lblod.data.gift/concept-schemes/33e233eb-6a9b-4141-b607-2ff72fe2ded2> as ?s)
    ?s ?p ?o.
  }
}

;

# step : correct some URI's
DELETE {
  GRAPH ?g {
    ?s <https://data.vlaanderen.be/ns/transactie#isInstantieVan> ?o.
  }
}
INSERT {
  GRAPH ?g {
    ?s <http://data.vlaanderen.be/ns/transactie#isInstantieVan> ?o.
  }
}
WHERE {
  GRAPH ?g {
    ?s <https://data.vlaanderen.be/ns/transactie#isInstantieVan> ?o.
  }
}

;

# step : remove application forms from aanvraag, bestuurseeneid en subsidiemaatregelaanbod
PREFIX pav: <http://purl.org/pav/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX dct: <http://purl.org/dc/terms/>

DELETE {
  GRAPH ?g {
    ?application pav:createdBy ?bestuurseenheid.
    ?application lblodSubsidie:subsidyMeasure ?aanbod.
    ?aanvraag <http://www.w3.org/ns/prov#used> ?application.
  }
}
INSERT {
  GRAPH ?g {
    ?consumptie dct:source ?application.
  }
}
WHERE{
  GRAPH ?g {
    ?application a lblodSubsidie:ApplicationForm.
    ?application pav:createdBy ?bestuurseenheid.
    ?application lblodSubsidie:subsidyMeasure ?aanbod.
    ?aanvraag <http://www.w3.org/ns/prov#used> ?application.
    ?consumptie prov:wasGeneratedBy ?aanvraag.
    ?consumptie m8g:hasParticipation ?participatie.
    ?parEenheid m8g:playsRole ?participatie.
  }
}

;
# step : drop criterium, stap from aanbod, will be added later
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>

DELETE {
  GRAPH ?g {
    ?s <http://purl.org/vocab/cpsv#follows> ?stap.
    ?s <http://data.europa.eu/m8g/hasCriterion> ?criterium.
  }
}
WHERE {
  GRAPH ?g {
    ?s a subsidie:SubsidiemaatregelAanbod.
    ?s <http://purl.org/vocab/cpsv#follows> ?stap.
    ?s <http://data.europa.eu/m8g/hasCriterion> ?criterium.
  }
}

;

# step : drop subsidieprocedure-stappen
DELETE {
  GRAPH ?g {
    ?s ?p ?o.
  }
}
WHERE {
  GRAPH ?g {
    ?s ?p <http://data.vlaanderen.be/ns/subsidie#Subsidieprocedurestap>.
    ?s ?p ?o.
  }
}

;

# step : remove m8g:PeriodeOfTime
PREFIX m8g: <http://data.europa.eu/m8g/>
DELETE {
  GRAPH ?g {
    ?s ?p ?o.
    ?referenceS ?referenceP ?s.
  }
}
WHERE {
  GRAPH ?g {
    ?s a m8g:PeriodOfTime.
    ?s ?p ?o.
    OPTIONAL { ?referenceS ?referenceP ?s. }
  }
}

;

# step : remove http://data.europa.eu/m8g/PeriodOfTime
DELETE {
  GRAPH ?g {
    ?s ?p ?o.
    ?referenceS ?referenceP ?s.
  }
}
WHERE {
  GRAPH ?g {
   ?s a <https://www.gleif.org/ontology/Base/Period>.
   ?s ?p ?o.
   OPTIONAL { ?referenceS ?referenceP ?s. }
  }
}

;

# step : migrate subsidieprocedurestappen, flow, flow-step
PREFIX mobiliteit: <https://data.vlaanderen.be/ns/mobiliteit#>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX xkos: <http://rdf-vocabulary.ddialliance.org/xkos#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX common: <http://www.w3.org/2007/uwa/context/common.owl#>
PREFIX transactie: <http://data.vlaanderen.be/ns/transactie#>

DELETE {
  GRAPH ?public {
    ?reeks <https://www.gleif.org/ontology/Base/hasEnd> ?endReeks;
      <https://www.gleif.org/ontology/Base/hasStart> ?startReeks.
  }
}
INSERT {
  GRAPH ?public {
    ?ps a subsidie:Subsidieprocedurestap;
      mu:uuid ?psUuid;
      dct:description "Aanvraag".

    ?aanbod cpsv:follows ?ps.
    ?reeks lblodSubsidie:heeftSubsidieprocedurestap ?ps.
    ?reeks common:active ?af.

    ?af a lblodSubsidie:ApplicationFlow;
      mu:uuid ?afUuid;
      xkos:belongsTo ?reeks.

      ?afs a lblodSubsidie:ApplicationStep;
        mu:uuid ?afsUuid;
        qb:order 0;
        dct:references ?ps;
        dct:isPartOf ?af.

    ?applicationform dct:isPartOf ?afs.

    ?periodReeks a <http://data.europa.eu/m8g/PeriodOfTime>;
      mu:uuid ?periodReeksUuid;
      <http://data.europa.eu/m8g/endTime> ?end;
      <http://data.europa.eu/m8g/startTime> ?start.
    ?reeks mobiliteit:periode ?periodReeks.

    ?periodPs a <http://data.europa.eu/m8g/PeriodOfTime>;
      mu:uuid ?periodPsUuid;
      <http://data.europa.eu/m8g/endTime> ?end;
      <http://data.europa.eu/m8g/startTime> ?start.

    ?ps mobiliteit:periode ?periodPs.
  }
}
WHERE {

  GRAPH ?public {
    ?reeks a lblodSubsidie:SubsidiemaatregelAanbodReeks.
    ?reeks mu:uuid ?reeksUuid.
    ?reeks <https://www.gleif.org/ontology/Base/hasEnd> ?endReeks;
      <https://www.gleif.org/ontology/Base/hasStart> ?startReeks.
    ?aanbod a subsidie:SubsidiemaatregelAanbod.
    ?aanbod lblodSubsidie:heeftReeks ?reeks.
  }

  BIND(SHA256(CONCAT(STR(?reeks), "subsidie:Subsidieprocedurestap")) as ?psUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/subsidieprocedurestappen/", ?psUuid)) AS ?ps) .

  BIND(SHA256(CONCAT(STR(?reeks), "lblodSubsidie:ApplicationFlow")) as ?afUuid) .
  BIND(IRI(CONCAT("http://lblod.data.info/id/subsidie-application-flows/", ?afUuid)) AS ?af) .

  BIND(SHA256(CONCAT(STR(?reeks), "lblodSubsidie:ApplicationStep")) as ?afsUuid) .
  BIND(IRI(CONCAT("http://lblod.data.info/id/subsidie-application-flow-steps/", ?afsUuid)) AS ?afs) .

  BIND(STRDT(CONCAT(STR(?startReeks), 'T03:00:00'), xsd:dateTime) as ?start)
  BIND(STRDT(CONCAT(STR(?endReeks), 'T03:00:00'), xsd:dateTime) as ?end)
  BIND(SHA256(CONCAT(STR(?reeks), "m8g:PeriodOfTime-reeks")) as ?periodReeksUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/periodes/", ?periodReeksUuid)) AS ?periodReeks) .
  BIND(SHA256(CONCAT(STR(?reeks), "m8g:PeriodOfTime-procedurestap")) as ?periodPsUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/periodes/", ?periodPsUuid)) AS ?periodPs) .
}

;

### TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error ###
DELETE {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5>  {
    ?s ?p ?o .
  }
}
WHERE {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5>  {
    ?s ?p ?o .
  }
}
### END OF TRICK ###
