PREFIX mobiliteit: <https://data.vlaanderen.be/ns/mobiliteit#>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

############################################################
## DELETE all period data for contact-tracing subsidies
DELETE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?series mobiliteit:periode ?claim .
        ?claim ?cp ?co .
        ?step mobiliteit:periode ?deadline .
        ?deadline ?dp ?do .
    }
} WHERE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        VALUES ?series {
            <http://lblod.data.gift/concepts/a43f99ce-710f-4e81-8aa1-d56660146765>
        }
        ?series
            mobiliteit:periode ?claim ;
            lblodSubsidie:heeftSubsidieprocedurestap ?step .

        ?step mobiliteit:periode ?deadline .

        ?claim ?cp ?co .
        ?deadline ?dp ?do .
    }
}
;
###################################################################
# INSERT claim & deadline for emergency contact-tracing, Winter
INSERT DATA {
    GRAPH <http://mu.semte.ch/graphs/public> {

        <http://data.lblod.info/id/periodes/07f5fe2e-210a-4046-9643-a690cdc4a5e9>
            a m8g:PeriodOfTime ;
            mu:uuid "07f5fe2e-210a-4046-9643-a690cdc4a5e9" ;
            m8g:startTime "2020-11-01T00:00:00"^^xsd:dateTime ;
            m8g:endTime "2021-03-31T00:00:00"^^xsd:dateTime .

        <http://lblod.data.gift/concepts/a43f99ce-710f-4e81-8aa1-d56660146765>
            mobiliteit:periode <http://data.lblod.info/id/periodes/07f5fe2e-210a-4046-9643-a690cdc4a5e9> .

        <http://data.lblod.info/id/periodes/ce89e6f8-5a72-4f5c-98dd-0f7128a6ca24>
            a m8g:PeriodOfTime ;
            mu:uuid "ce89e6f8-5a72-4f5c-98dd-0f7128a6ca24" ;
            m8g:startTime "2021-01-31T00:00:00"^^xsd:dateTime ;
            m8g:endTime "2021-06-04T00:00:00"^^xsd:dateTime .

        <http://data.lblod.info/id/subsidieprocedurestappen/84464f95b2278b3394d0178478d8772d9dde89480827bfddf49db25a677c9491> # Request
            mobiliteit:periode <http://data.lblod.info/id/periodes/ce89e6f8-5a72-4f5c-98dd-0f7128a6ca24> .
    }
}