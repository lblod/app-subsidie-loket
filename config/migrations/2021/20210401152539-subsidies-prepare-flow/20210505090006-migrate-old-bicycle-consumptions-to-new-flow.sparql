PREFIX mobiliteit: <https://data.vlaanderen.be/ns/mobiliteit#>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX cube: <http://purl.org/linked-data/cube#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX xkos: <http://rdf-vocabulary.ddialliance.org/xkos#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX common: <http://www.w3.org/2007/uwa/context/common.owl#>

####################################################################
### TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error
INSERT DATA {
    GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5> {
        <http://dummy/application/6c68539a-f6de-4488-8c1b-1c182e07a142>
            a lblodSubsidie:ApplicationForm ;
            dct:source <config://forms/bicycle-infrastructure/versions/20210420163405-initial-version/form.ttl> ;
            dct:isPartOf <http://lblod.data.info/id/subsidie-application-flow-steps/eb633b90fee29738872ee370ec24d60da792c9bdf5b97e125c7a34653d5e4397> .

        <http://dummy/consumption/8af40a49-afca-4ea8-b123-c4ef3b00eee8>
            a subsidie:SubsidiemaatregelConsumptie ;
            cpsv:follows <http://lblod.data.info/id/subsidie-application-flows/1a7fb92ca5e7a948c687bda5754233cbb654d1c324dda5dbe9fce6ed4fbad163> ;
            adms:status <http://lblod.data.gift/concepts/2ea29fbf-6d46-4f08-9343-879282a9f484> ;
            dct:source <http://dummy/application/6c68539a-f6de-4488-8c1b-1c182e07a142> .

    }
}
;
####################################################################
### UPDATE old flow active consumptions to new flow
DELETE {
    GRAPH ?org {
        ?consumption
            adms:status ?status ;
            common:active ?active .

        ?form
            dct:source ?spec ;
            dct:isPartOf ?step .
    }
} INSERT {
    GRAPH ?org {
        ?consumption adms:status ?newStatus .
        ?consumption common:active ?newActive .

        ?form
            dct:source <config://forms/bicycle-infrastructure/proposal/versions/20210420163405-initial-version/form.ttl> ;
            dct:source <config://forms/bicycle-infrastructure/proposal/versions/20210420163405-initial-version/form.json> ;
            dct:isPartOf <http://lblod.data.info/id/subsidie-application-flow-steps/f135ee39-fff1-4cae-9b87-bf3cd9098b3e> .
    }
} WHERE {
    GRAPH ?org {
        ?consumption
            a subsidie:SubsidiemaatregelConsumptie ;
            cpsv:follows <http://lblod.data.info/id/subsidie-application-flows/1a7fb92ca5e7a948c687bda5754233cbb654d1c324dda5dbe9fce6ed4fbad163> ;
            adms:status ?status ;
            dct:source ?form .
        OPTIONAL {?consumption common:active ?active .} .

        ?form
            a lblodSubsidie:ApplicationForm ;
            dct:source <config://forms/bicycle-infrastructure/versions/20210420163405-initial-version/form.ttl> ;
            dct:source ?spec ;
            dct:isPartOf ?step .
    }
    BIND(IF(?status = <http://lblod.data.gift/concepts/2ea29fbf-6d46-4f08-9343-879282a9f484>, <http://lblod.data.gift/concepts/c849ca98-455d-4f31-9e95-a3d9d06e4497>, <http://lblod.data.gift/concepts/6373b454-22b6-4b65-b98f-3d86541f2fcf>) as ?newStatus)
    BIND(IF(?status = <http://lblod.data.gift/concepts/2ea29fbf-6d46-4f08-9343-879282a9f484>, <http://lblod.data.info/id/subsidie-application-flow-steps/eb633b90fee29738872ee370ec24d60da792c9bdf5b97e125c7a34653d5e4397> ,<http://lblod.data.info/id/subsidie-application-flow-steps/f135ee39-fff1-4cae-9b87-bf3cd9098b3e>) as ?newActive)
}
;
######################################################################
# TRICK TO AVOID QUERY CRASHING IF NO TRIPLE FOUND - sprintf error
DELETE {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5>  {
    ?s ?p ?o .
  }
}
WHERE {
  GRAPH <http://dummy/data/graph/0dc61f81-6ec6-4e16-80d3-26a61079c3c5>  {
    ?s ?p ?o .
  }
}
;
######################################################################
# REMOVE old spec form files
DELETE{
    GRAPH <http://mu.semte.ch/graphs/public> {
        <config://forms/bicycle-infrastructure/versions/20210420163405-initial-version/form.ttl> ?tp ?to .
        <config://forms/bicycle-infrastructure/versions/20210420163405-initial-version/form.json> ?jp ?jo .
    }
}WHERE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        <config://forms/bicycle-infrastructure/versions/20210420163405-initial-version/form.ttl> ?tp ?to .
        <config://forms/bicycle-infrastructure/versions/20210420163405-initial-version/form.json> ?jp ?jo .
    }
}