# Some forms have multiple objective tables due to a bug in ember-submission-form-fields.
# Those duplicates are empty tables with just a type, a uuid and a `validObjectiveTable` boolean.
# They can safely be removed as there is always a full functional table linked to the form, which will remain.

DELETE {
  GRAPH ?g {
    ?form <http://lblod.data.gift/vocabularies/subsidie/bicycle-infrastructure#objectiveTable> ?table .
    ?table ?p ?o .
  }
} WHERE {
  GRAPH ?g {
    ?form a <http://lblod.data.gift/vocabularies/subsidie/ApplicationForm> ;
      <http://lblod.data.gift/vocabularies/subsidie/bicycle-infrastructure#objectiveTable> ?table .

    ?table ?p ?o .

    FILTER NOT EXISTS { ?table <http://lblod.data.gift/vocabularies/subsidie/bicycle-infrastructure#objectiveEntry> ?entry . }
  }
}
