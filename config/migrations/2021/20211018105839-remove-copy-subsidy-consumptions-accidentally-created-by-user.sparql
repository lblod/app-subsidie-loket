PREFIX subsidie: <http://data.vlaanderen.be/ns/subsidie#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX lblodSubsidie: <http://lblod.data.gift/vocabularies/subsidie/>
PREFIX schema: <http://schema.org/>
PREFIX bicycleInfrastructure: <http://lblod.data.gift/vocabularies/subsidie/bicycle-infrastructure#>

# Remove Participation
DELETE {
    GRAPH ?g {
        ?participation ?p ?o .
        ?org m8g:playsRole ?participation .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            m8g:hasParticipation ?participation .
        ?participation ?p ?o .
        ?org m8g:playsRole ?participation .
    }
}
;
# Remove Decision Uploads
DELETE {
    GRAPH ?g {
        ?form lblodSubsidie:decisionUpload ?node .
        ?node dct:hasPart ?fileRef .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            dct:source ?form .
        ?form lblodSubsidie:decisionUpload ?node .
        ?node dct:hasPart ?fileRef .
    }
}
;
# Remove Pictures
DELETE {
    GRAPH ?g {
        ?form lblodSubsidie:picturesUpload ?node .
        ?node dct:hasPart ?fileRef .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            dct:source ?form .
        ?form lblodSubsidie:picturesUpload ?node .
        ?node dct:hasPart ?fileRef .
    }
}
;
# Remove Report Uploads
DELETE {
   GRAPH ?g {
        ?form lblodSubsidie:reportUpload ?node .
        ?node dct:hasPart ?fileRef .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            dct:source ?form .
        ?form lblodSubsidie:reportUpload ?node .
        ?node dct:hasPart ?fileRef .
    }
}
;
# Remove Objective Table (heavy)
DELETE {
    GRAPH ?g {
        ?form bicycleInfrastructure:objectiveTable ?table .

        ?table bicycleInfrastructure:objectiveEntry ?entry ;
               ?tp ?to .
        ?entry ?ep ?eo .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            dct:source ?form .
        ?form bicycleInfrastructure:objectiveTable ?table .

        ?table bicycleInfrastructure:objectiveEntry ?entry ;
               ?tp ?to .
        ?entry ?ep ?eo .
    }
}
;
# Remove Cost Estimate Table
DELETE {
    GRAPH ?g {
        ?form bicycleInfrastructure:estimatedCostTable ?table .

        ?table bicycleInfrastructure:estimatedCostEntry ?entry ;
               ?tp ?to .
        ?entry ?ep ?eo .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            dct:source ?form .
        ?form bicycleInfrastructure:estimatedCostTable ?table .

        ?table bicycleInfrastructure:estimatedCostEntry ?entry ;
               ?tp ?to .
        ?entry ?ep ?eo .
    }
}
;
# Remove Contact Info
DELETE {
    GRAPH ?g {
        ?form schema:contactPoint ?contactPoint .
        ?contactPoint ?p ?o .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            dct:source ?form .
        ?form schema:contactPoint ?contactPoint .
        ?contactPoint ?p ?o .
    }
}
;
# Remove Form Data
DELETE {
    GRAPH ?g {
        ?form ?p ?o .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            dct:source ?form .
        ?form ?p ?o .
    }
}
;
# Remove Consumption
DELETE {
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            ?p ?o .
    }
} WHERE {
    VALUES ?consumptionId {
        "6167F46BE5754600090000C8"
        "616826BDE57546000900018F"
        "61681ED2E575460009000147"
    }
    GRAPH ?g {
        ?consumptionURI a subsidie:SubsidiemaatregelConsumptie ;
            mu:uuid ?consumptionId ;
            ?p ?o .
    }
}