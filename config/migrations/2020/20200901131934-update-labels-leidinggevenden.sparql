PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
   ?bestuursorgaan skos:prefLabel ?oldLabel.
 }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
   ?bestuursorgaan skos:prefLabel ?updatedLabel.
 }
}
WHERE {
  GRAPH ?g {
  ?bestuursorgaan a besluit:Bestuursorgaan.
  ?bestuursorgaan skos:prefLabel ?oldLabel.
  ?bestuursorgaan besluit:bestuurt ?bestuurseenheid.

  ?bestuurseenheid a besluit:Bestuurseenheid.
  ?bestuurseenheid skos:prefLabel ?besLabel.
  ?bestuurseenheid besluit:classificatie ?eenheidClassificatie.
  ?eenheidClassificatie skos:prefLabel ?eenheidClassLabel.

  ?bestuursorgaan besluit:classificatie ?classificatie.
  ?classificatie skos:prefLabel ?classLabel.

  BIND(CONCAT(?classLabel, " ", ?eenheidClassLabel, " ", ?besLabel) as ?updatedLabel)

  FILTER(?classificatie IN (
  <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/39854196-f214-4688-87a1-d6ad12baa2fa>,
  <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/62644b9c-4514-41dd-a660-4c35257f2b35>,
  <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/11f0af9e-016c-4e0b-983a-d8bc73804abc>,
  <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/ed40469e-3b6f-4f38-99ba-18912ee352b0>))
 }
}