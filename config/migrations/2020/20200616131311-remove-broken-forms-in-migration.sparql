# Inzending voor toezicht, which were in status Concept before the migration and have been
# send after migration, were not correclty converted into a submission.
# This migration, removes the task and meta data related to the conversion, so the migration service
# can run it again.

PREFIX toezicht: <http://mu.semte.ch/vocabularies/ext/supervision/>
PREFIX nuao: <http://www.semanticdesktop.org/ontologies/2010/01/25/nuao#>
DELETE {
  GRAPH ?g {
   ?task ?taskP ?taskO.
 }
 GRAPH ?h {
   ?submission ?submissionP ?submissionO.
   ?submittedDoc ?submittedDocP ?submittedDocO.
   ?formData ?formDataP ?formDataO.
 }
}
WHERE {
  GRAPH ?g {
    ?task nuao:involves ?inzending.
    OPTIONAL { ?task ?taskP ?taskO. }
  }
  GRAPH ?h {
    ?inzending a toezicht:InzendingVoorToezicht.
    ?submission <http://purl.org/dc/terms/source> ?inzending.
    ?submission ?submissionP ?submissionO.

    OPTIONAL {
       ?submission <http://purl.org/dc/terms/subject> ?submittedDoc.
       ?submittedDoc ?submittedDocP ?submittedDocO.
    }
    OPTIONAL {
      ?submission <http://www.w3.org/ns/prov#generated> ?formData.
      ?formData ?formDataP ?formDataO.
    }
  }
  FILTER( ?submission IN (
  <http://data.lblod.info/submissions/297bbbe0-9c90-11ea-bf84-d3a6db0d88ac>,
  <http://data.lblod.info/submissions/eeaf3c30-9c80-11ea-bf84-d3a6db0d88ac>,
  <http://data.lblod.info/submissions/ace80e80-9c99-11ea-bf84-d3a6db0d88ac>,
  <http://data.lblod.info/submissions/f1ea5d70-9c95-11ea-bf84-d3a6db0d88ac>,
  <http://data.lblod.info/submissions/974fa510-9c99-11ea-bf84-d3a6db0d88ac>,
  <http://data.lblod.info/submissions/d10b9ef0-9c83-11ea-bf84-d3a6db0d88ac>
  ))
}
