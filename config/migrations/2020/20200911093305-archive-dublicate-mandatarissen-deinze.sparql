#####
# This migration will 'archive' double mandatarissen entries.
# All ?s ?p ?o from the dublicate entries will be first removed (they were in the wrong graph anyway).
# Then owl:sameAs will be added so we don't encounter unexisting urls.
# This will be required e.g. in case these remove URI's were already used somewhere else.
####

PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

# Migration will remove members RMW and Vast Bureau FROM eenheid gemeente.

DELETE {
  GRAPH <http://mu.semte.ch/graphs/organizations/d93451bf-e89a-4528-80f3-f0a1c19361a8/LoketLB-mandaatGebruiker> {
    ?mandataris ?mandatarisP ?mandatarisO.
  }
}
WHERE {

  # GEMEENTE DEINZE
  GRAPH <http://mu.semte.ch/graphs/organizations/d93451bf-e89a-4528-80f3-f0a1c19361a8/LoketLB-mandaatGebruiker> {
     #BIND (<http://data.lblod.info/id/mandatarissen/5C38711657753A00090003B0> as ?mandataris)
     ?mandataris mandaat:isBestuurlijkeAliasVan ?persoon.
     ?persoon persoon:gebruikteVoornaam ?voornaam.

     ?mandataris org:holds ?mandaat.
     ?mandataris ?mandatarisP ?mandatarisO.
  }

  ?mandaat org:role ?rol.
  ?rol skos:prefLabel ?label.
  ?bot  org:hasPost ?mandaat.
  ?bot mandaat:isTijdspecialisatieVan ?bo.
  ?bo besluit:classificatie ?bClass.
  ?bClass skos:prefLabel ?bLabel.
  ?bo besluit:bestuurt ?eenheid.
  ?eenheid besluit:classificatie ?eClass.
  ?eClass skos:prefLabel ?eLabel.

  FILTER(?bClass IN (
    <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000007>, # RMW
    <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/5ab0e9b8a3b2ca7c5e000008>  # Vast Bureau
  ))
}

;

# Since these removed mandatrissen were double enttries we set same as relationshit
# see https://binnenland.atlassian.net/browse/DL-2061


INSERT DATA {

   # OCMW DEINZE
   GRAPH <http://mu.semte.ch/graphs/organizations/a4fd24f3-cc6d-4ea5-91c7-efa02d3b970f/LoketLB-mandaatGebruiker> {
     <http://data.lblod.info/id/mandatarissen/5C38718457753A00090003C5> owl:sameAs <http://data.lblod.info/id/mandatarissen/5C3AF01E57753A0009000043> .
     <http://data.lblod.info/id/mandatarissen/5C3AF01E57753A0009000043> owl:sameAs <http://data.lblod.info/id/mandatarissen/5C38718457753A00090003C5> .

    <http://data.lblod.info/id/mandatarissen/5C3871A057753A00090003CB> owl:sameAs <http://data.lblod.info/id/mandatarissen/5C3AF00557753A0009000042> .
    <http://data.lblod.info/id/mandatarissen/5C3AF00557753A0009000042> owl:sameAs <http://data.lblod.info/id/mandatarissen/5C3871A057753A00090003CB>.

    <http://data.lblod.info/id/mandatarissen/5C3870D457753A00090003AC> owl:sameAs <http://data.lblod.info/id/mandatarissen/5C3AEF6E57753A000900003F> .
    <http://data.lblod.info/id/mandatarissen/5C3AEF6E57753A000900003F> owl:sameAs <http://data.lblod.info/id/mandatarissen/5C3870D457753A00090003AC> .

    <http://data.lblod.info/id/mandatarissen/5C38711657753A00090003B0> owl:sameAs <http://data.lblod.info/id/mandatarissen/5C3AEF5957753A000900003E> .
    <http://data.lblod.info/id/mandatarissen/5C3AEF5957753A000900003E> owl:sameAs <http://data.lblod.info/id/mandatarissen/5C38711657753A00090003B0> .

    <http://data.lblod.info/id/mandatarissen/5C3641369E5B64000900046A> owl:sameAs <http://data.lblod.info/id/mandatarissen/5C3AF45657753A0009000068> .
    <http://data.lblod.info/id/mandatarissen/5C3AF45657753A0009000068>  owl:sameAs <http://data.lblod.info/id/mandatarissen/5C3641369E5B64000900046A>.

    <http://data.lblod.info/id/mandatarissen/5C36F3359E5B6400090004EC> owl:sameAs <http://data.lblod.info/id/mandatarissen/5C3AF42857753A0009000066> .
    <http://data.lblod.info/id/mandatarissen/5C3AF42857753A0009000066>  owl:sameAs <http://data.lblod.info/id/mandatarissen/5C36F3359E5B6400090004EC>.

   }
}