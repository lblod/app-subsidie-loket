PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>

# Remove bestuurseenheid

DELETE {
  GRAPH ?g {
    <http://data.lblod.info/id/bestuurseenheden/fa3f4f3c-02be-4a6a-ac1e-68264ba14d60> ?p ?o.
  }
}

WHERE {
  GRAPH ?g {
    <http://data.lblod.info/id/bestuurseenheden/fa3f4f3c-02be-4a6a-ac1e-68264ba14d60> ?p ?o.
  }
}

;

# Remove bestuursorganen, bestuursorganen in tijd and bestuursfuncties

DELETE {
  GRAPH ?g {
    ?bestuursorgaan <http://data.vlaanderen.be/ns/besluit#bestuurt> <http://data.lblod.info/id/bestuurseenheden/fa3f4f3c-02be-4a6a-ac1e-68264ba14d60> ;
      ?boP ?boO .
    ?bestuursorgaanInTijd <http://data.lblod.info/vocabularies/leidinggevenden/heeftBestuursfunctie> ?bestuursfunctie ;
      ?botP ?botO .
    ?bestuursfunctie ?beP ?beO .
  }
}

WHERE {
  GRAPH ?g {
    ?bestuursorgaan <http://data.vlaanderen.be/ns/besluit#bestuurt> <http://data.lblod.info/id/bestuurseenheden/fa3f4f3c-02be-4a6a-ac1e-68264ba14d60> ;
      ?boP ?boO ;
      ^mandaat:isTijdspecialisatieVan ?bestuursorgaanInTijd .
    ?bestuursorgaanInTijd <http://data.lblod.info/vocabularies/leidinggevenden/heeftBestuursfunctie> ?bestuursfunctie ;
      ?botP ?botO .
    ?bestuursfunctie ?beP ?beO .
  }
}

;

# Remove mock user

DELETE DATA {
  GRAPH <http://mu.semte.ch/graphs/public> {
    <http://data.lblod.info/id/persoon/3fdf3973537ef42bfa84c21d2f3dc44e> <http://xmlns.com/foaf/0.1/member> <http://data.lblod.info/id/bestuurseenheden/fa3f4f3c-02be-4a6a-ac1e-68264ba14d60>.
  }
}
;
DELETE {
  GRAPH <http://mu.semte.ch/graphs/organizations/fa3f4f3c-02be-4a6a-ac1e-68264ba14d60> {
    <http://data.lblod.info/id/account/9fb51c25499c6e8e62435a307de3dece> ?accP ?accO.
    <http://data.lblod.info/id/persoon/3fdf3973537ef42bfa84c21d2f3dc44e> ?p <http://data.lblod.info/id/account/9fb51c25499c6e8e62435a307de3dece>.
    <http://data.lblod.info/id/persoon/3fdf3973537ef42bfa84c21d2f3dc44e> ?persP ?persO.
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/organizations/fa3f4f3c-02be-4a6a-ac1e-68264ba14d60> {
    <http://data.lblod.info/id/account/9fb51c25499c6e8e62435a307de3dece> ?accP ?accO.
    <http://data.lblod.info/id/persoon/3fdf3973537ef42bfa84c21d2f3dc44e> ?p <http://data.lblod.info/id/account/9fb51c25499c6e8e62435a307de3dece>.
    <http://data.lblod.info/id/persoon/3fdf3973537ef42bfa84c21d2f3dc44e> ?persP ?persO.
  }
}
