#################################################
# cleans data from mandaten extractor
# TO RUN IN LOKET
#################################################
PREFIX nmo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nmo#>
PREFIX toezichtReport: <http://mu.semte.ch/vocabularies/ext/supervision/reporting/>
PREFIX toezicht: <http://mu.semte.ch/vocabularies/ext/supervision/>
PREFIX validation: <http://mu.semte.ch/vocabularies/validation/>
PREFIX bbcdr: <http://mu.semte.ch/vocabularies/ext/bbcdr/>
PREFIX export: <http://mu.semte.ch/vocabularies/ext/export/>
PREFIX dbpedia: <http://dbpedia.org/ontology/>
PREFIX schema: <http://schema.org/>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX pav: <http://purl.org/pav/>
PREFIX nao: <http://www.semanticdesktop.org/ontologies/2007/08/15/nao#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX regorg: <https://www.w3.org/ns/regorg#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX person: <http://www.w3.org/ns/person#>
PREFIX adms: <http://www.w3.org/ns/adms#>
PREFIX dul: <http://www.ontologydesignpatterns.org/ont/dul/DUL.owl#>
PREFIX cpsv: <http://purl.org/vocab/cpsv#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX m8g: <http://data.europa.eu/m8g/>
PREFIX eli: <http://data.europa.eu/eli/ontology#>
PREFIX generiek: <http://data.vlaanderen.be/ns/generiek#>
PREFIX persoon: <http://data.vlaanderen.be/ns/persoon#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX tmp: <http://mu.semte.ch/vocabularies/tmp/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX rm: <http://mu.semte.ch/vocabularies/logical-delete/>
PREFIX typedLiterals: <http://mu.semte.ch/vocabularies/typed-literals/>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX app: <http://mu.semte.ch/app/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>


#cleans up messy dates
DELETE {
  GRAPH <http://mu.semte.ch/graphs/mandaten/bilzen/20190808> {
    ?s ?p "0001-01-01"^^xsd:date.
 }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/mandaten/bilzen/20190808> {
    ?s ?p "0001-01-01"^^xsd:date.
  }
}

;

# some prefix issue -> for now move them to what is used in loket
DELETE {
  GRAPH <http://mu.semte.ch/graphs/mandaten/bilzen/20190808> {
     ?fractie <http://www.w3.org/ns/regorg#legalName> ?naam.
   }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/mandaten/bilzen/20190808> {
     ?fractie <https://www.w3.org/ns/regorg#legalName> ?naam.
   }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/mandaten/bilzen/20190808> {
     ?fractie <http://www.w3.org/ns/regorg#legalName> ?naam.
   }
}

;

#remove new line chars in name
DELETE {
  GRAPH <http://mu.semte.ch/graphs/mandaten/bilzen/20190808> {
     ?fractie <https://www.w3.org/ns/regorg#legalName> ?oldName.
   }
}
INSERT {
  GRAPH <http://mu.semte.ch/graphs/mandaten/bilzen/20190808> {
     ?fractie <https://www.w3.org/ns/regorg#legalName> ?strippedName.
   }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/mandaten/bilzen/20190808> {
     ?fractie <https://www.w3.org/ns/regorg#legalName> ?oldName.
   }
   FILTER(REGEX(?oldName, "\n", "i"))
   BIND(REPLACE(?oldName, "\n", "", "i") AS ?strippedName)
}

;

#if einddata schepen not exits, provide default
INSERT {
  GRAPH <http://mu.semte.ch/graphs/mandaten/bilzen/20190808> {
     ?mandataris mandaat:einde "2024-12-31"^^xsd:date.
   }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/mandaten/bilzen/20190808> {
    ?mandataris a mandaat:Mandataris.
    ?mandataris org:holds ?mandaat.
  }
  ?mandaat org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000014>. #schepen
  FILTER NOT EXISTS {
    ?mandataris mandaat:einde ?einde.
  }
}

;
