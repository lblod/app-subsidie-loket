PREFIX nmo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nmo#>
PREFIX fni: <http://www.semanticdesktop.org/ontologies/2007/03/22/fni#>
PREFIX nie: <http://www.semanticdesktop.org/ontologies/2007/03/22/nie#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX schema: <http://schema.org/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/system/email> {
    ?email nmo:sentDate ?messageReceived.
  }
}
WHERE {
  GRAPH <http://mu.semte.ch/graphs/system/email> {
        ?mailfolder a nfo:Folder;
            nie:title  "sentbox".
        ?email a nmo:Email;
            nmo:isPartOf ?mailfolder.
        FILTER NOT EXISTS { ?email nmo:sentDate ?sentEmail. }
  }
  GRAPH ?g {
        ?message a schema:Message;
            ext:notificatieEmail ?email;
            schema:dateReceived ?messageReceived.
  }
  FILTER(STRSTARTS(STR(?g), "http://mu.semte.ch/graphs/organizations/"))
  FILTER(STRENDS(STR(?g), "/LoketLB-berichtenGebruiker"))
}
