PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX mu:      <http://mu.semte.ch/vocabularies/core/>
PREFIX org:     <http://www.w3.org/ns/org#>
PREFIX sd:      <http://www.w3.org/ns/sparql-service-description#>
PREFIX nie:     <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ext:     <http://mu.semte.ch/vocabularies/ext/>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX pav:  <http://purl.org/pav/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX eli: <http://data.europa.eu/eli/ontology#>
PREFIX dct: <http://purl.org/dc/terms/>

# burgemeesters should not live in public graph

INSERT {
  GRAPH ?eenheidGraph {
    ?mandataris ?p ?o.
   }
}
WHERE {
  ?eenheid mu:uuid ?eenheidUuid.
  ?orgaan besluit:bestuurt ?eenheid.
  ?orgaanInTijd mandaat:bindingStart "2019-01-01"^^xsd:date.
  ?orgaanInTijd mandaat:isTijdspecialisatieVan ?orgaan.
  ?orgaanInTijd org:hasPost ?mandaat.

  GRAPH <http://mu.semte.ch/graphs/public>  {
    ?mandataris a mandaat:Mandataris.
    ?mandataris org:holds ?mandaat .
                    # burgemeester
    ?mandaat org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000013> .

    ?mandataris ?p ?o.
  }

  BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", STR(?eenheidUuid),"/LoketLB-mandaatGebruiker")) AS ?eenheidGraph).

}

;

DELETE {
  GRAPH <http://mu.semte.ch/graphs/public> {
  ?mandataris ?p ?o.
 }
}
WHERE {
  ?eenheid mu:uuid ?eenheidUuid.
  ?orgaan besluit:bestuurt ?eenheid.
  ?orgaanInTijd mandaat:bindingStart "2019-01-01"^^xsd:date.
  ?orgaanInTijd mandaat:isTijdspecialisatieVan ?orgaan.
  ?orgaanInTijd org:hasPost ?mandaat.

GRAPH <http://mu.semte.ch/graphs/public>  {
  ?mandataris a mandaat:Mandataris.
  ?mandataris org:holds ?mandaat .
                  # burgemeester
  ?mandaat org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000013> .

  ?mandataris ?p ?o.
  }

  BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", STR(?eenheidUuid),"/LoketLB-mandaatGebruiker")) AS ?eenheidGraph).

}