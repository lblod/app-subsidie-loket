PREFIX schema: <http://schema.org/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX nmo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nmo#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>

INSERT {
    GRAPH ?g {
        ?bericht schema:recipient ?bestuurseenheid.
    }
}
WHERE {
    GRAPH ?g {
        ?bericht a schema:Message;
            schema:dateReceived ?ontvangen;
            schema:sender ?van.
        FILTER NOT EXISTS { ?bericht schema:recipient ?naar. }
    }
    FILTER(STRSTARTS(STR(?g), "http://mu.semte.ch/graphs/organizations/"))
    FILTER(STRENDS(STR(?g), "/LoketLB-berichtenGebruiker"))
    
    BIND(REPLACE(STR(?g), 'http://mu.semte.ch/graphs/organizations/','') AS ?tmpUuid).
    BIND(REPLACE(STR(?tmpUuid), '/LoketLB-berichtenGebruiker','') AS ?bestuurseenheidUuid).
    
    GRAPH ?h {
        ?bestuurseenheid a besluit:Bestuurseenheid;
            mu:uuid ?bestuurseenheidUuid.
    }
}

