PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX mu:      <http://mu.semte.ch/vocabularies/core/>
PREFIX org:     <http://www.w3.org/ns/org#>
PREFIX sd:      <http://www.w3.org/ns/sparql-service-description#>
PREFIX nie:     <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ext:     <http://mu.semte.ch/vocabularies/ext/>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX pav:  <http://purl.org/pav/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX eli: <http://data.europa.eu/eli/ontology#>
PREFIX dct: <http://purl.org/dc/terms/>

#########
# move fracties
########
INSERT {
  GRAPH ?eenheidGraph {
    ?fractie a mandaat:Fractie.
    ?fractie <http://mu.semte.ch/vocabularies/ext/isFractietype> ?type.
    ?fractie <http://www.w3.org/ns/org#memberOf> ?bestuursorgaan.
    ?fractie <http://www.w3.org/ns/org#linkedTo> ?eenheid.
    ?fractie <https://www.w3.org/ns/regorg#legalName> ?naam.
    ?fractie mu:uuid ?uuid.

    ?fractie ext:generatedFrom ext:mandatenExtractorService.
   }
}

WHERE {

  GRAPH <http://mu.semte.ch/application> {
    ?fractie a mandaat:Fractie.
    ?fractie mu:uuid ?uuid.
    ?fractie ext:generatedFrom ext:mandatenExtractorService.
    ?fractie <http://www.w3.org/ns/org#memberOf> ?bestuursorgaan.
    ?fractie <http://www.w3.org/ns/org#linkedTo> ?eenheid.

    OPTIONAL { ?fractie <http://mu.semte.ch/vocabularies/ext/isFractietype> ?type. }
    OPTIONAL { ?fractie <https://www.w3.org/ns/regorg#legalName> ?naam. } #note the S

  }

  GRAPH <http://mu.semte.ch/graphs/public> {
    ?eenheid mu:uuid ?eenheidUuid.
  }
  BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", STR(?eenheidUuid),"/LoketLB-mandaatGebruiker")) AS ?eenheidGraph).

}