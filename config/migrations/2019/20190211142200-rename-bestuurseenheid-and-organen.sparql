PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

DELETE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?bestuurseenheid skos:prefLabel ?label.
        ?bestuurorgaan skos:prefLabel ?bestuurorgaanLabel.
    }
}
INSERT {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?bestuurseenheid skos:prefLabel ?updatedLabel.
        ?bestuurorgaan skos:prefLabel ?updatedBestuursorganLabel.
    }
}
WHERE {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?bestuurseenheid a besluit:Bestuurseenheid;
            ^besluit:bestuurt ?bestuurorgaan;
            skos:prefLabel ?label.
        FILTER regex(?label, "INFRAX LIMBURG o.v.|Infrax West|INFRAX")
        ?bestuurorgaan skos:prefLabel ?bestuurorgaanLabel.
        BIND(REPLACE(?label, "INFRAX|Infrax", "Fluvius", "i") AS ?updatedLabel)
        BIND(REPLACE(?bestuurorgaanLabel, "INFRAX|Infrax", "Fluvius", "i") AS ?updatedBestuursorganLabel)
    }
}
;
DELETE {
    GRAPH ?g {
        ?persoon foaf:familyName ?naam.
    }
}
INSERT {
    GRAPH ?g {
        ?persoon foaf:familyName ?updatedNaam.
    }
}
WHERE {
    GRAPH ?g {
    ?persoon a foaf:Person;
            foaf:familyName ?naam;
            foaf:account ?account.
    ?account a foaf:OnlineAccount;
            foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>.
    FILTER regex(?naam, "INFRAX LIMBURG o.v.|Infrax West|INFRAX")
    BIND(REPLACE(?naam, "INFRAX|Infrax", "Fluvius", "i") AS ?updatedNaam)
    }
}
