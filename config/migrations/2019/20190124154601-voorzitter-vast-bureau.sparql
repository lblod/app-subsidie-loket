PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX mu:      <http://mu.semte.ch/vocabularies/core/>
PREFIX org:     <http://www.w3.org/ns/org#>
PREFIX sd:      <http://www.w3.org/ns/sparql-service-description#>
PREFIX nie:     <http://www.semanticdesktop.org/ontologies/2007/01/19/nie#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX ext:     <http://mu.semte.ch/vocabularies/ext/>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX pav:  <http://purl.org/pav/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX nfo: <http://www.semanticdesktop.org/ontologies/2007/03/22/nfo#>
PREFIX eli: <http://data.europa.eu/eli/ontology#>
PREFIX dct: <http://purl.org/dc/terms/>

# note: voorzitter vast bureau is burgemeester
INSERT {
  GRAPH ?rmwGraph {
    ?newMandataris a mandaat:Mandataris;
                org:holds ?voorzitter;
                mandaat:isBestuurlijkeAliasVan ?persoon;
                mu:uuid ?newMandatarisUuid;
                mandaat:start ?start;
                mandaat:einde ?einde;
                mandaat:status ?status.
   }
}
WHERE {
  ?voorzitter org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000018> . #voorzitter vast bureau
  ?rmwInTijd org:hasPost ?voorzitter.
  ?rmwInTijd mandaat:bindingStart "2019-01-01"^^xsd:date.
  ?rmwInTijd mandaat:isTijdspecialisatieVan ?rmwOrgaan.
  ?rmwEenheid mu:uuid ?rwmUuid.
                                    #ocmw
  ?rmwEenheid besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/5ab0e9b8a3b2ca7c5e000002>.
  ?rmwOrgaan besluit:bestuurt ?rmwEenheid.
  ?rmwEenheid besluit:werkingsgebied ?werkingsgebied.
  ?eenheid besluit:werkingsgebied ?werkingsgebied.
  ?orgaan besluit:bestuurt ?eenheid.
  ?orgaanInTijd mandaat:bindingStart "2019-01-01"^^xsd:date.
  ?orgaanInTijd mandaat:isTijdspecialisatieVan ?orgaan.
  ?orgaanInTijd org:hasPost ?mandaat.

  ?mandataris a mandaat:Mandataris.
  ?mandataris org:holds ?mandaat .
                  # burgemeester
  ?mandaat org:role <http://data.vlaanderen.be/id/concept/BestuursfunctieCode/5ab0e9b8a3b2ca7c5e000013> .
  ?mandataris mandaat:isBestuurlijkeAliasVan ?persoon .

  OPTIONAL { ?mandataris mandaat:start ?start } .
  OPTIONAL { ?mandataris mandaat:einde ?einde } .
  OPTIONAL { ?mandataris mandaat:status ?status } .

  BIND(IRI(CONCAT("http://mu.semte.ch/graphs/organizations/", STR(?rwmUuid),"/LoketLB-mandaatGebruiker")) AS ?rmwGraph).
  BIND(SHA256(CONCAT(STR(?mandataris), "voorzitter-vast-bureau", "f2cb34e0-1a5e-11e9-8241-6bba9d726b9c")) as ?newMandatarisUuid) .
  BIND(IRI(CONCAT("http://data.lblod.info/id/mandatarissen/", ?newMandatarisUuid)) AS ?newMandataris) .

  FILTER NOT EXISTS {
    ?anExistingVoorzitter org:holds ?voorzitter.
  }
}