PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX ext: <http://mu.semte.ch/vocabularies/ext/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

INSERT DATA {
  GRAPH <http://mu.semte.ch/graphs/public> {
    <http://data.lblod.info/id/bestuurseenheden/1bbd0f7a-664c-46bd-8ca0-891801b81962> a besluit:Bestuurseenheid ;
       mu:uuid "1bbd0f7a-664c-46bd-8ca0-891801b81962" ;
       skos:prefLabel "Zorgbedrijf Sint-Truiden" ;
       dcterms:identifier "0687742074" ;
       ext:kbonummer "0687742074" ;
       besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuurseenheidClassificatieCode/cc4e2d67-603b-4784-9b61-e50bac1ec089> ;
       besluit:werkingsgebied <http://data.lblod.info/id/werkingsgebieden/6f382bd7c8e0bd81ffb83f0d13048dfd35525bde9fc0677a4a7a5780cc8e2614> . #TODO: werkingsgebied sint truiden

   <http://data.lblod.info/id/bestuursorganen/a3b3e2d4-fe97-4770-9e35-b9751b41b7bb> besluit:bestuurt <http://data.lblod.info/id/bestuurseenheden/1bbd0f7a-664c-46bd-8ca0-891801b81962> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "a3b3e2d4-fe97-4770-9e35-b9751b41b7bb" ;
       skos:prefLabel "Raad van bestuur Zorgbedrijf Sint-Truiden" ;
       besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/013cc838-173a-4657-b1ae-b00c048df943> .

   <http://data.lblod.info/id/bestuursorganen/2e10e616-c023-462a-afa2-6fdcc40c3b7f> mandaat:isTijdspecialisatieVan <http://data.lblod.info/id/bestuursorganen/a3b3e2d4-fe97-4770-9e35-b9751b41b7bb> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "2e10e616-c023-462a-afa2-6fdcc40c3b7f" ;
       mandaat:bindingStart "2012-01-01"^^<http://www.w3.org/2001/XMLSchema#date> .

   <http://data.lblod.info/id/bestuursorganen/214768c3-edae-4dd8-897f-81aa48fb54fe> besluit:bestuurt <http://data.lblod.info/id/bestuurseenheden/1bbd0f7a-664c-46bd-8ca0-891801b81962> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "214768c3-edae-4dd8-897f-81aa48fb54fe" ;
       skos:prefLabel "Algemene vergadering Zorgbedrijf Sint-Truiden" ;
       besluit:classificatie <http://data.vlaanderen.be/id/concept/BestuursorgaanClassificatieCode/b52094ff-21a2-4da8-8dbe-f513365d1528> .

   <http://data.lblod.info/id/bestuursorganen/838e0686-5faa-48a4-903f-8f6abc94aa4c> mandaat:isTijdspecialisatieVan <http://data.lblod.info/id/bestuursorganen/214768c3-edae-4dd8-897f-81aa48fb54fe> ;
       a besluit:Bestuursorgaan ;
       mu:uuid "838e0686-5faa-48a4-903f-8f6abc94aa4c";
       mandaat:bindingStart "2012-01-01"^^<http://www.w3.org/2001/XMLSchema#date> .
  }
}

;

INSERT {
  GRAPH ?g {
     ?persoon a foaf:Person;
           mu:uuid ?uuidPersoon;
           foaf:firstName ?classificatie;
           foaf:familyName ?naam;
           foaf:member <http://data.lblod.info/id/bestuurseenheden/1bbd0f7a-664c-46bd-8ca0-891801b81962> ;
           foaf:account ?account.
     ?account a foaf:OnlineAccount;
           mu:uuid ?uuidAccount;
           foaf:accountServiceHomepage <https://github.com/lblod/mock-login-service>;
           ext:sessionRole "LoketLB-toezichtGebruiker", "LoketLB-bbcdrGebruiker", "LoketLB-mandaatGebruiker", "LoketLB-berichtenGebruiker", "LoketLB-leidinggevendenGebruiker" .
  }
} WHERE {
     <http://data.lblod.info/id/bestuurseenheden/1bbd0f7a-664c-46bd-8ca0-891801b81962> a besluit:Bestuurseenheid;
     skos:prefLabel ?naam;
     besluit:classificatie/skos:prefLabel ?classificatie.
     BIND(CONCAT(?classificatie, " ", ?naam) as ?volledigeNaam)
     BIND(MD5(?volledigeNaam) as ?uuidPersoon)
     BIND(MD5(CONCAT(?volledigeNaam,"ACCOUNT")) as ?uuidAccount)
     BIND(IRI(CONCAT("http://data.lblod.info/id/persoon/", ?uuidPersoon)) AS ?persoon)
     BIND(IRI(CONCAT("http://data.lblod.info/id/account/", ?uuidAccount)) AS ?account)

     VALUES ?g {
        <http://mu.semte.ch/graphs/organizations/1bbd0f7a-664c-46bd-8ca0-891801b81962>
        <http://mu.semte.ch/graphs/public> 
     } 
}

